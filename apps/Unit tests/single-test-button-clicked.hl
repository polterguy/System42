
/*
 * Evaluates a single unit test, according to the specified [_event] and [_file] arguments.
 * The [_event] is the button that was clicked to evaluate the test.
 */
_html
eval-x:x:/+/*
sys42.utilities.execute-lambda-file:@TESTS/evaluate-single-test.hl
  _file:x:/../*/_file?value
if:x:/-/*/_error

  /*
   * Error while evaluating unit test file, setting result
   * to ell error "messages" + "stack-trace" of exception,
   * and making sure we signal in context value that error 
   * occurred
   */
  for-each:x:/../*/sys42.utilities.execute-lambda-file/*/_error?value
    p5.html.html-encode:x:/./*/_dp?value
    set:x:/../*/_html?value
      src:@"{0}<pre class=""error"">{1}</pre>"
        :x:/../*/_html?value
        :x:/..for-each/*/p5.html.html-encode?value
  p5.web.context.set:sys42.unit-test-errors
    src:bool:true

  /*
   * Setting unit tests table row to class "error", and button
   * CSS class to warning
   */
  p5.web.widgets.find
    _file-row:x:/../*/_file?value
  p5.web.widgets.property.set:x:/-/*/*?value
    class:error
  p5.web.widgets.property.set:x:/../*/_event?value
    class:btn btn-warning form-control
    disabled
else

  /*
   * Success while evaluating unit test file, no errors.
   * Setting unit tests table row to class "success" and
   * button CSS class to success
   */
  p5.web.widgets.find
    _file-row:x:/../*/_file?value
  p5.web.widgets.property.set:x:/-/*/*?value
    class:success
  p5.web.widgets.property.set:x:/../*/_event?value
    class:btn btn-success form-control
    disabled

/*
 * Updating unit test count
 * Notice, this is only done when evaluating all unit tests
 */
p5.web.session.get:sys42.unit-test-count
if:x:/-/*?value
  +:x:/./-/*?value
    _:x:/../*/sys42.utilities.execute-lambda-file/*(/_success|/_error)?count
  p5.web.session.set:sys42.unit-test-count
    src:x:/./-?value

/*
 * Providing textual feedback to user about state of unit tests, into 
 * the "result" table cell.
 * This will create a numbered list to the user, of all successfully evaluated
 * unit tests, and their names.
 */
p5.web.widgets.find
  _file-result:x:/../*/_file?value
p5.string.join:x:/../*/sys42.utilities.execute-lambda-file/*/_success?value
  sep:"\r\n</li><li>"
p5.web.widgets.property.set:x:/-2/*/*?value
  innerValue:@"{0}<pre><ol><li>{1}</li></ol></pre>"
    :x:/../*/_html?value
    :x:/././-?value

