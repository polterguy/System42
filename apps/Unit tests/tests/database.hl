
/*
 * Unit tests for p5.data events, such as [p5.data.delete], [p5.data.insert] and [p5.data.select].
 */


/*
 * Insert data tests.
 */


Insert simple static item
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:Thomas Hansen
  if
    fetch:x:/0/0?value
      p5.data.select:x:/*/*/sys42.tests.insert/*/name?value
    !=:Thomas Hansen
    throw:Assert error, [p5.data.insert] misbehaved


Insert items resulting from an expression
  p5.data.delete:x:/*/*/sys42.tests.insert
  _items
    sys42.tests.insert
      name:Thomas Hansen
  p5.data.insert:x:/@_items/*
  if
    fetch:x:/0/0?value
      p5.data.select:x:/*/*/sys42.tests.insert/*/name?value
    !=:Thomas Hansen
    throw:Assert error, [p5.data.insert] misbehaved


Insert items returns affected items
  p5.data.delete:x:/*/*/sys42.tests.insert
  _items
    sys42.tests.insert
      name:Thomas Hansen
    sys42.tests.insert
      name:John Doe
  p5.data.insert:x:/@_items/*
  if:x:/@p5.data.insert?value
    !=:int:2
    throw:Assert error, [p5.data.insert] misbehaved


Insert same ID throws
  _success:bool:false
  p5.data.delete:x:/*/*/sys42.tests.insert-1
  p5.data.delete:x:/*/*/sys42.tests.insert-2
  try
    p5.data.insert
      sys42.tests.insert-1:THIS_IS_THE_SAME_ID
        name:Thomas Hansen
      sys42.tests.insert-2:THIS_IS_THE_SAME_ID
        name:John Doe
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, [p5.data.insert] misbehaved


Insert same ID in different invocations throws
  _success:bool:false
  p5.data.delete:x:/*/*/sys42.tests.insert-1
  p5.data.delete:x:/*/*/sys42.tests.insert-2
  p5.data.insert
    sys42.tests.insert-1:THIS_IS_THE_SAME_ID
      name:Thomas Hansen
  try
    p5.data.insert
      sys42.tests.insert-2:THIS_IS_THE_SAME_ID
        name:John Doe
  catch
    set:x:/@_success?value
      src:boool:true
  if:x:/@_success?value
    not
    throw:Assert error, [p5.data.insert] misbehaved


Insert empty name throws
  _success:bool:false
  try
    p5.data.insert
      ""
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, [p5.data.insert] misbehaved


/*
 * Update data tests.
 */


Update simple data value
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:foo
  p5.data.update:x:/*/*/sys42.tests.insert/*/name?value
    src:bar
  if
    fetch:x:/0/0?value
      p5.data.select:x:/*/*/sys42.tests.insert/*/name?value
    !=:bar
    throw:Assert error, [p5.data.update] misbehaved


Update simple data name
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:foo
  p5.data.update:x:/*/*/sys42.tests.insert/*/name?name
    src:bar
  if
    fetch:x:/0/0?name
      p5.data.select:x:/*/*/sys42.tests.insert/0?name
    !=:bar
    throw:Assert error, [p5.data.update] misbehaved


Update returns affected items
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:foo
    sys42.tests.insert
      name:foo
  p5.data.update:x:/*/*/sys42.tests.insert/*/name?value
    src:bar
  if:x:/@p5.data.update?value
    !=:int:2
    throw:Assert error, [p5.data.update] misbehaved


Update data does not change root node ID by default
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:foo
  p5.data.select:x:/*/*/sys42.tests.insert
  p5.data.update:x:/*/*/sys42.tests.insert
    src
      sys42.tests.insert
        name:bar
  p5.data.select:x:/*/*/sys42.tests.insert
  if:x:/../*/p5.data.select/[0,1]/*?value
    !=:x:/../*/p5.data.select/[1,2]/*?value
    or:x:/../*/p5.data.select/[0,1]/*?value
      !=:x:/@p5.data.insert/*?value
    throw:Assert error, [p5.data.update] misbehaved


Update data can change root node ID
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:foo
  p5.data.select:x:/*/*/sys42.tests.insert
  p5.data.update:x:/*/*/sys42.tests.insert
    src
      sys42.tests.insert:THIS_IS_A_BRAND_NEW_ID
        name:bar
  p5.data.select:x:/*/*/sys42.tests.insert
  if:THIS_IS_A_BRAND_NEW_ID
    !=:x:/../*/p5.data.select/[1,2]/*?value
    throw:Assert error, [p5.data.update] misbehaved


Update data throws when ID exists
  _success:bool:false
  p5.data.delete:x:/*/*/=THIS_IS_THE_SAME_ID
  p5.data.insert
    sys42.tests.insert
      name:foo
    sys42.tests.insert:THIS_IS_THE_SAME_ID
      name:foo
  try
    p5.data.update:x:/*/*/sys42.tests.insert/[0,1]
      src
        test.foo:THIS_IS_THE_SAME_ID
          name:bar
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, [p5.data.update] misbehaved


Update data with explicit same ID no throw
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert:THIS_IS_THE_SAME_ID
      name:foo
  p5.data.update:x:/*/*/sys42.tests.insert
    src
      sys42.tests.insert:THIS_IS_THE_SAME_ID
        name:bar
  p5.data.select:x:/*/*/sys42.tests.insert
  if:x:/@p5.data.select/*?value
    !=:THIS_IS_THE_SAME_ID
    throw:Assert error, [p5.data.update] misbehaved


Update multiple records keeps ID
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:foo
    sys42.tests.insert
      name:foo
  p5.data.update:x:/*/*/sys42.tests.insert
    src
      sys42.tests.insert
        name:bar
  p5.data.select:x:/*/*/sys42.tests.insert
  if:x:/@p5.data.insert/*?value
    !=:x:/@p5.data.select/*?value
    throw:Assert error, [p5.data.update] misbehaved


/*
 * Delete data tests.
 */


Delete data returns affected items
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:Thomas Hansen
    sys42.tests.insert
      name:John Doe
  p5.data.delete:x:/*/*/sys42.tests.insert
  if:x:/@p5.data.delete?value
    !=:int:2
    throw:Assert error, [p5.data.delete] misbehaved


Delete data on file nodes throws
  _success:bool:false
  try
    p5.data.delete:x:/*
  catch
    set:x:/@_success?value
      bool:true
  if:x:/@_success?value
    not
    throw:Assert error, [p5.data.delete] misbehaved


Delete entire database throws
  _success:bool:false
  try
    p5.data.delete:x:
  catch
    set:x:/@_success?value
      bool:true
  if:x:/@_success?value
    not
    throw:Assert error, [p5.data.delete] misbehaved


/*
 * Misc.
 */


Select count from database
  p5.data.delete:x:/*/*/sys42.tests.insert
  p5.data.insert
    sys42.tests.insert
      name:Thomas Hansen
    sys42.tests.insert
      name:Thomas Hansen
  p5.data.select:x:/*/*/sys42.tests.insert?count
  if:x:/@p5.data.select?value
    !=:int:2
    throw:Assert error, [p5.data.select] misbehaved
