
/*
 * Unit tests for widgets.
 */


Create and delete widget, verify exists
  p5.web.widgets.create-container:sys42.tests.widget-1
  p5.web.widgets.exists:sys42.tests.widget-1
  p5.web.widgets.delete:sys42.tests.widget-1
  p5.web.widgets.exists:sys42.tests.widget-1
  if:x:/../*/p5.web.widgets.exists/[0,1]/0?value
    not
    or:x:/../*/p5.web.widgets.exists/[1,2]/0?value
    throw:Assert error, widgets misbehaved


Create widget with same ID twice throws
  _success:bool:false
  p5.web.widgets.create-container:sys42.tests.widget-2
  try
    p5.web.widgets.create-container:sys42.tests.widget-2
  catch
    set:x:/@_success?value
      src:bool:true
  p5.web.widgets.delete:sys42.tests.widget-2
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create widget with bad parent throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-3
      parent:non-existing-widget
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Widget special properties yields correct values
  p5.web.widgets.create-container:sys42.tests.widget-4
    parent:content
    element:span
    class:foo
    visible:false
    position:0
  p5.web.widgets.property.get:sys42.tests.widget-4
    parent
    element
    class
    visible
    position
  p5.web.widgets.delete:sys42.tests.widget-4
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-4
    parent:content
    element:span
    class:foo
    visible:bool:false
    position:int:0"
    throw:Assert error, widgets misbehaved


Widget properties changed with static values
  p5.web.widgets.create-container:sys42.tests.widget-5
    visible:false
    class:foo
  p5.web.widgets.property.set:sys42.tests.widget-5
    visible:true
    class:bar
  p5.web.widgets.property.get:sys42.tests.widget-5
    visible
    class
  p5.web.widgets.delete:sys42.tests.widget-5
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-5
    visible:bool:true
    class:bar"
    throw:Assert error, widgets misbehaved


Widget find for single constant property
  p5.web.widgets.create-container:sys42.tests.widget-6
    class:foo
  p5.web.widgets.find
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  cnt
    container:sys42.tests.widget-6"
    throw:Assert error, widgets misbehaved


Widget find for position 0
  p5.web.widgets.create-container:sys42.tests.widget-6
    widgets
      literal:test-1
        element:small
      literal:test-2
        element:small
  p5.web.widgets.find:sys42.tests.widget-6
    position:0
    element:small
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  sys42.tests.widget-6
    literal:test-1"
    throw:Assert error, widgets misbehaved


Widget find for position 1
  p5.web.widgets.create-container:sys42.tests.widget-6
    widgets
      literal:test-1
        element:small
      literal:test-2
        element:small
  p5.web.widgets.find:sys42.tests.widget-6
    position:1
    element:small
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  sys42.tests.widget-6
    literal:test-2"
    throw:Assert error, widgets misbehaved


Widget find for position not existing
  p5.web.widgets.create-container:sys42.tests.widget-6
    widgets
      literal:test-1
        element:small
      literal:test-2
        element:small
  p5.web.widgets.find:sys42.tests.widget-6
    position:3
    element:small
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find"
    throw:Assert error, widgets misbehaved


Widget find for before
  p5.web.widgets.create-container:sys42.tests.widget-6
    widgets
      literal:test-1
      literal:test-2
  p5.web.widgets.find:sys42.tests.widget-6
    before:test-2
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  sys42.tests.widget-6
    literal:test-1"
    throw:Assert error, widgets misbehaved


Widget find for after
  p5.web.widgets.create-container:sys42.tests.widget-6
    widgets
      literal:test-1
      literal:test-2
  p5.web.widgets.find:sys42.tests.widget-6
    after:test-1
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  sys42.tests.widget-6
    literal:test-2"
    throw:Assert error, widgets misbehaved


Widget find for parent
  p5.web.widgets.create-container:sys42.tests.widget-6
    widgets
      literal:test-1
      literal:test-2
  p5.web.widgets.find:sys42.tests.widget-6
    parent:sys42.tests.widget-6
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  sys42.tests.widget-6
    literal:test-1
    literal:test-2"
    throw:Assert error, widgets misbehaved


Widget find for multiple constant properties
  p5.web.widgets.create-container:sys42.tests.widget-7
    class:foo
    bar:xyz
  p5.web.widgets.find
    class:foo
    bar:xyz
  p5.web.widgets.delete:sys42.tests.widget-7
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  cnt
    container:sys42.tests.widget-7"
    throw:Assert error, widgets misbehaved


Widget find multiple widgets for multiple constant properties
  p5.web.widgets.create-container:sys42.tests.widget-7
    class:foo
    bar:xyz
  p5.web.widgets.create-container:sys42.tests.widget-8
    class:foo
    bar:xyz
  p5.web.widgets.find
    class:foo
    bar:xyz
  p5.web.widgets.delete:sys42.tests.widget-7
  p5.web.widgets.delete:sys42.tests.widget-8
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  cnt
    container:sys42.tests.widget-7
    container:sys42.tests.widget-8"
    throw:Assert error, widgets misbehaved


Widget find for single expression property
  p5.web.widgets.create-container:sys42.tests.widget-8
    class:foo
  _class:foo
  p5.web.widgets.find
    class:x:/@_class?value
  p5.web.widgets.delete:sys42.tests.widget-8
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find
  cnt
    container:sys42.tests.widget-8"
    throw:Assert error, widgets misbehaved


Widget find-like for single constant property
  p5.web.widgets.create-container:sys42.tests.widget-6
    class:foo-bar
  p5.web.widgets.find-like
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find-like
    !=:node:@"p5.web.widgets.find-like
  cnt
    container:sys42.tests.widget-6"
    throw:Assert error, widgets misbehaved


Widget find-like respecting start widget
  p5.web.widgets.create-container:sys42.tests.widget-6
    parent:cnt
    class:foo-bar
  p5.web.widgets.find-like:content
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find-like
    !=:node:@"p5.web.widgets.find-like"
    throw:Assert error, widgets misbehaved


Widget find respecting start widget
  p5.web.widgets.create-container:sys42.tests.widget-6
    parent:cnt
    class:foo-bar
  p5.web.widgets.find:content
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-6
  if:x:/@p5.web.widgets.find
    !=:node:@"p5.web.widgets.find"
    throw:Assert error, widgets misbehaved


Create container widget with innerValue throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-9
      innerValue:foo
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create literal widget with widgets collection throws
  _success:bool:false
  try
    p5.web.widgets.create-literal:sys42.tests.widget-11
      widgets
        literal
          innerValue:xxx
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create void widget with widgets throws
  _success:bool:false
  try
    p5.web.widgets.create-void:sys42.tests.widget-12
      widgets
        literal
          innerValue:xxx
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create void widget with innerValue throws
  _success:bool:false
  try
    p5.web.widgets.create-void:sys42.tests.widget-13
      innerValue:xxx
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Default container widget element is div
  p5.web.widgets.create-container:sys42.tests.widget-14
  p5.web.widgets.property.get:sys42.tests.widget-14
    element
  p5.web.widgets.delete:sys42.tests.widget-14
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-14
    element:div"
    throw:Assert error, widgets misbehaved


Default literal widget element is p
  p5.web.widgets.create-literal:sys42.tests.widget-15
  p5.web.widgets.property.get:sys42.tests.widget-15
    element
  p5.web.widgets.delete:sys42.tests.widget-15
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-15
    element:p"
    throw:Assert error, widgets misbehaved


Default void widget element is input
  p5.web.widgets.create-void:sys42.tests.widget-16
  p5.web.widgets.property.get:sys42.tests.widget-16
    element
  p5.web.widgets.delete:sys42.tests.widget-16
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-16
    element:input"
    throw:Assert error, widgets misbehaved


Default parent widget is cnt
  p5.web.widgets.create-void:sys42.tests.widget-17
  p5.web.widgets.property.get:sys42.tests.widget-17
    parent
  p5.web.widgets.delete:sys42.tests.widget-17
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-17
    parent:cnt"
    throw:Assert error, widgets misbehaved


Create widget with two children widgets with same ID throws and is atomic
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-18
      widgets
        literal:foo
        literal:foo
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    or
      fetch:x:/0/0?value
        p5.web.widgets.exists:sys42.tests.widget-18
    throw:Assert error, widgets misbehaved


Create widget before another widget
  p5.web.widgets.create-container:sys42.tests.widget-19
  p5.web.widgets.create-container:sys42.tests.widget-20
    parent:sys42.tests.widget-19
  p5.web.widgets.create-container:sys42.tests.widget-21
    before:sys42.tests.widget-20
  p5.web.widgets.get-children:sys42.tests.widget-19
  p5.web.widgets.delete:sys42.tests.widget-19
  if:x:/@p5.web.widgets.get-children
    !=:node:@"p5.web.widgets.get-children
  sys42.tests.widget-19
    container:container:sys42.tests.widget-21
    container:container:sys42.tests.widget-20"
    throw:Assert error, widgets misbehaved


Create widget after another widget
  p5.web.widgets.create-container:sys42.tests.widget-22
  p5.web.widgets.create-container:sys42.tests.widget-23
    parent:sys42.tests.widget-22
  p5.web.widgets.create-container:sys42.tests.widget-24
    after:sys42.tests.widget-23
  p5.web.widgets.get-children:sys42.tests.widget-22
  p5.web.widgets.delete:sys42.tests.widget-22
  if:x:/@p5.web.widgets.get-children
    !=:node:@"p5.web.widgets.get-children
  sys42.tests.widget-22
    container:container:sys42.tests.widget-23
    container:container:sys42.tests.widget-24"
    throw:Assert error, widgets misbehaved


Create widget with generic event and innerValue
  p5.web.widgets.create:sys42.tests.widget-25
    innerValue:Foo bar
  p5.web.widgets.list:sys42.tests.widget-25
  p5.web.widgets.delete:sys42.tests.widget-25
  if:x:/@p5.web.widgets.list/0?name
    !=:literal
    throw:Assert error, widgets misbehaved


Create widget with generic event and widgets
  p5.web.widgets.create:sys42.tests.widget-26
    widgets
  p5.web.widgets.list:sys42.tests.widget-26
  p5.web.widgets.delete:sys42.tests.widget-26
  if:x:/@p5.web.widgets.list/0?name
    !=:container
    throw:Assert error, widgets misbehaved


Create widget with generic event and neither widgets nor innerValue
  p5.web.widgets.create:sys42.tests.widget-27
  p5.web.widgets.list:sys42.tests.widget-27
  p5.web.widgets.delete:sys42.tests.widget-27
  if:x:/@p5.web.widgets.list/0?name
    !=:void
    throw:Assert error, widgets misbehaved


Clear widget clears children collection
  p5.web.widgets.create:sys42.tests.widget-28
    widgets
      literal:foo
      literal:bar
  p5.web.widgets.get-children:sys42.tests.widget-28
  p5.web.widgets.clear:sys42.tests.widget-28
  p5.web.widgets.get-children:sys42.tests.widget-28
  p5.web.widgets.delete:sys42.tests.widget-28
  if:x:/../*/p5.web.widgets.get-children/[0,1]/*/*?count
    !=:int:2
    or:x:/../*/p5.web.widgets.get-children/[1,2]/*/*?count
      !=:int:0
    throw:Assert error, widgets misbehaved


Supplying both parent and before throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-29
      parent:cnt
      before:content
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Supplying both parent and after throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-30
      parent:cnt
      after:content
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Supplying both before and after throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-31
      before:content
      after:content
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Supplying both before and position throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-32
      before:content
      position:0
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Supplying both after and position throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-33
      after:content
      position:0
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Create widget with parent and position
  p5.web.widgets.create-container:sys42.tests.widget-34
  p5.web.widgets.create-container:sys42.tests.widget-35
    parent:sys42.tests.widget-34
  p5.web.widgets.create-container:sys42.tests.widget-36
    parent:sys42.tests.widget-34
    position:0
  p5.web.widgets.get-children:sys42.tests.widget-34
  p5.web.widgets.delete:sys42.tests.widget-34
  if:x:/@p5.web.widgets.get-children
    !=:node:@"p5.web.widgets.get-children
  sys42.tests.widget-34
    container:container:sys42.tests.widget-36
    container:container:sys42.tests.widget-35"
    throw:Assert error, widgets misbehaved


Create visible widget with oninit event
  p5.web.viewstate.set:sys42.tests.widget-test-1
  p5.web.widgets.create-container:sys42.tests.widget-37
    oninit
      p5.web.viewstate.set:sys42.tests.widget-test-1
        src:bool:true
  p5.web.widgets.delete:sys42.tests.widget-37
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-1
    not
    throw:Assert error, widgets misbehaved


Create invisible widget with oninit event
  p5.web.viewstate.set:sys42.tests.widget-test-2
  p5.web.widgets.create-container:sys42.tests.widget-38
    visible:false
    oninit
      p5.web.viewstate.set:sys42.tests.widget-test-2
        src:bool:true
  p5.web.widgets.delete:sys42.tests.widget-38
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-2
    throw:Assert error, widgets misbehaved


Create invisible widget with oninit event, make visible after creation
  p5.web.viewstate.set:sys42.tests.widget-test-3
  p5.web.widgets.create-container:sys42.tests.widget-39
    visible:false
    oninit
      p5.web.viewstate.set:sys42.tests.widget-test-3
        src:bool:true
  p5.web.widgets.property.set:sys42.tests.widget-39
    visible:true
  p5.web.widgets.delete:sys42.tests.widget-39
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-3
    not
    throw:Assert error, widgets misbehaved


Create widget with oninit event invoking lambda event
  p5.web.viewstate.set:sys42.tests.widget-test-4
  p5.web.widgets.create-container:sys42.tests.widget-40
    oninit
      sys42.tests.widgets.lambda-1
    events
      sys42.tests.widgets.lambda-1
        p5.web.viewstate.set:sys42.tests.widget-test-4
          src:bool:true
  p5.web.widgets.delete:sys42.tests.widget-40
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-4
    not
    throw:Assert error, widgets misbehaved


Create widget with lambda event which is invoked after creation
  p5.web.viewstate.set:sys42.tests.widget-test-5
  p5.web.widgets.create-container:sys42.tests.widget-41
    events
      sys42.tests.widgets.lambda-2
        p5.web.viewstate.set:sys42.tests.widget-test-5
          src:bool:true
  sys42.tests.widgets.lambda-2
  p5.web.widgets.delete:sys42.tests.widget-41
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-5
    not
    throw:Assert error, widgets misbehaved


Create two widgets handling the same lambda event
  p5.web.widgets.create-container:sys42.tests.widget-42
    events
      sys42.tests.widgets.lambda-3
        return
          one
  p5.web.widgets.create-container:sys42.tests.widget-43
    events
      sys42.tests.widgets.lambda-3
        return
          two
  sys42.tests.widgets.lambda-3
  p5.web.widgets.delete:sys42.tests.widget-42
  p5.web.widgets.delete:sys42.tests.widget-43
  if:x:/@sys42.tests.widgets.lambda-3
    !=:node:@"sys42.tests.widgets.lambda-3
  one
  two"
    throw:Assert error, widgets misbehaved


Create two widgets handling the same lambda event, delete one before invocation
  p5.web.widgets.create-container:sys42.tests.widget-44
    events
      sys42.tests.widgets.lambda-4
        return
          one
  p5.web.widgets.create-container:sys42.tests.widget-45
    events
      sys42.tests.widgets.lambda-4
        return
          two
  p5.web.widgets.delete:sys42.tests.widget-44
  sys42.tests.widgets.lambda-4
  p5.web.widgets.delete:sys42.tests.widget-45
  if:x:/@sys42.tests.widgets.lambda-4
    !=:node:@"sys42.tests.widgets.lambda-4
  two"
    throw:Assert error, widgets misbehaved


Create widget with child having lambda event, which is invoked after creation
  p5.web.viewstate.set:sys42.tests.widget-test-6
  p5.web.widgets.create-container:sys42.tests.widget-46
    widgets
      literal
        events
          sys42.tests.widgets.lambda-5
            p5.web.viewstate.set:sys42.tests.widget-test-6
              src:bool:true
  sys42.tests.widgets.lambda-5
  p5.web.widgets.delete:sys42.tests.widget-46
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-6
    not
    throw:Assert error, widgets misbehaved


Create invisible widget, with invisible child, with lambda event, invoked after creation

  // Notive, even though [oninit] is not invoked on invisible widgets, lambda events are handled!
  p5.web.viewstate.set:sys42.tests.widget-test-7
  p5.web.widgets.create-container:sys42.tests.widget-47
    visible:false
    widgets
      literal
        visible:false
        events
          sys42.tests.widgets.lambda-6
            p5.web.viewstate.set:sys42.tests.widget-test-7
              src:bool:true
  sys42.tests.widgets.lambda-6
  p5.web.widgets.delete:sys42.tests.widget-47
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-7
    not
    throw:Assert error, widgets misbehaved


Create visible widget with oninit event, making sure _event is passed correctly in
  p5.web.viewstate.set:sys42.tests.widget-test-8
  p5.web.widgets.create-container:sys42.tests.widget-48
    oninit
      p5.web.viewstate.set:sys42.tests.widget-test-8
        src:x:/../*/_event?value
  p5.web.widgets.delete:sys42.tests.widget-48
  if
    fetch:x:/0/0?value
      p5.web.viewstate.get:sys42.tests.widget-test-8
    !=:sys42.tests.widget-48
    throw:Assert error, widgets misbehaved


Child widget with parent throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-49
      widgets
        literal
          parent:content
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Child widget with before throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-49
      widgets
        literal
          before:content
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved
    throw:Assert error, widgets misbehaved


Child widget with after throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-49
      widgets
        literal
          after:content
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Child widget with _parent throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-49
      widgets
        literal
          _parent:content
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Child widget with position throws
  _success:bool:false
  try
    p5.web.widgets.create-container:sys42.tests.widget-49
      widgets
        literal
          position:0
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, widgets misbehaved


Widget properties can take expressions
  _data
    parent:content
    element:span
    class:foo
    visible:false
  p5.web.widgets.create-container:sys42.tests.widget-50
    parent:x:/@_data/*/parent?value
    element:x:/@_data/*/element?value
    class:x:/@_data/*/class?value
    visible:x:/@_data/*/visible?value
  p5.web.widgets.property.get:sys42.tests.widget-50
    parent
    element
    class
    visible
  p5.web.widgets.delete:sys42.tests.widget-50
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-50
    parent:content
    element:span
    class:foo
    visible:bool:false"
    throw:Assert error, widgets misbehaved


Child widget properties can take expressions
  _data
    element:span
    class:foo
    visible:false
  p5.web.widgets.create:sys42.tests.widget-51
    widgets
      literal:sys42.tests.widget-52
        element:x:/@_data/*/element?value
        class:x:/@_data/*/class?value
        visible:x:/@_data/*/visible?value
  p5.web.widgets.property.get:sys42.tests.widget-52
    parent
    element
    class
    visible
  p5.web.widgets.delete:sys42.tests.widget-52
  if:x:/@p5.web.widgets.property.get
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-52
    parent:sys42.tests.widget-51
    element:span
    class:foo
    visible:bool:false"
    throw:Assert error, widgets misbehaved


Retrieve widget's Ajax event
  p5.web.widgets.create:sys42.tests.widget-53
    onclick
      foo
  p5.web.widgets.ajax-events.get:sys42.tests.widget-53
    onclick
  p5.web.widgets.delete:sys42.tests.widget-53
  if:x:/@p5.web.widgets.ajax-events.get
    !=:node:@"p5.web.widgets.ajax-events.get
  sys42.tests.widget-53
    onclick
      _event:sys42.tests.widget-53
      foo"
    throw:Assert error, widgets misbehaved


Delete widget's Ajax event
  p5.web.widgets.create:sys42.tests.widget-54
    onclick
      foo
  p5.web.widgets.ajax-events.set:sys42.tests.widget-54
    onclick
  p5.web.widgets.ajax-events.get:sys42.tests.widget-54
    onclick
  p5.web.widgets.delete:sys42.tests.widget-54
  if:x:/@p5.web.widgets.ajax-events.get
    !=:node:@"p5.web.widgets.ajax-events.get"
    throw:Assert error, widgets misbehaved


Invoke widget's Ajax event
  p5.web.session.set:sys42.tests.session-value-1
  p5.web.widgets.create:sys42.tests.widget-53.2
    onclick
      p5.web.session.set:sys42.tests.session-value-1
        src:x:/../*/_event?value
  p5.web.widgets.ajax-events.raise:sys42.tests.widget-53.2
    onclick
  p5.web.widgets.delete:sys42.tests.widget-53.2
  p5.web.session.get:sys42.tests.session-value-1
  if:x:/@p5.web.session.get
    !=:node:@"p5.web.session.get
  sys42.tests.session-value-1:sys42.tests.widget-53.2"
    throw:Assert error, widgets misbehaved


Multiple widget lambda events returning value
  p5.web.widgets.create:sys42.tests.widget-55
    widgets
      literal
        events
          sys42.tests.lambda-7
            return:x
      literal
        events
          sys42.tests.lambda-7
            return:y
  sys42.tests.lambda-7
  p5.web.widgets.delete:sys42.tests.widget-55
  if:x:/@sys42.tests.lambda-7
    !=:node:@"sys42.tests.lambda-7:y"
    throw:Assert error, widgets misbehaved


Multiple widget lambda events returning value given value argument
  p5.web.widgets.create:sys42.tests.widget-56
    widgets
      literal
        events
          sys42.tests.lambda-8
            return:{0}-bar-1
              :x:/../*/_arg?value
      literal
        events
          sys42.tests.lambda-8
            return:{0}-bar-2
              :x:/../*/_arg?value
  sys42.tests.lambda-8:foo
  p5.web.widgets.delete:sys42.tests.widget-56
  if:x:/@sys42.tests.lambda-8
    !=:node:@"sys42.tests.lambda-8:foo-bar-2"
    throw:Assert error, widgets misbehaved


Copy lambda event from widget, making sure _event is wired up correctly
  p5.web.widgets.create:sys42.tests.widget-57
    events
      sys42.tests.lambda-9
        return:x:/../*/_event?value
  p5.web.widgets.create:sys42.tests.widget-58
  p5.web.widgets.lambda-events.get:sys42.tests.widget-57
    sys42.tests.lambda-9
  add:x:/+
    src:x:/@p5.web.widgets.lambda-events.get/*/*
  p5.web.widgets.lambda-events.set:sys42.tests.widget-58
  p5.web.widgets.lambda-events.set:sys42.tests.widget-57
    sys42.tests.lambda-9
  sys42.tests.lambda-9
  p5.web.widgets.delete:sys42.tests.widget-57
  p5.web.widgets.delete:sys42.tests.widget-58
  if:x:/@sys42.tests.lambda-9?value
    !=:sys42.tests.widget-58
    throw:Assert error, widgets misbehaved


List lambda events for widget
  p5.web.widgets.create:sys42.tests.widget-59
    events
      sys42.tests.lambda-10
        foo
      sys42.tests.lambda-11
        foo
      sys42.tests.lambda-12
  p5.web.widgets.lambda-events.list:sys42.tests.widget-59
  p5.web.widgets.delete:sys42.tests.widget-59
  if:x:/@p5.web.widgets.lambda-events.list
    !=:node:@"p5.web.widgets.lambda-events.list
  sys42.tests.widget-59
    sys42.tests.lambda-10
    sys42.tests.lambda-11"
    throw:Assert error, widgets misbehaved


Delete lambda event before listing events for widget
  p5.web.widgets.create:sys42.tests.widget-59
    events
      sys42.tests.lambda-10
        foo
      sys42.tests.lambda-11
        foo
  p5.web.widgets.lambda-events.set:sys42.tests.widget-59
    sys42.tests.lambda-10
  p5.web.widgets.lambda-events.list:sys42.tests.widget-59
  p5.web.widgets.delete:sys42.tests.widget-59
  if:x:/@p5.web.widgets.lambda-events.list
    !=:node:@"p5.web.widgets.lambda-events.list
  sys42.tests.widget-59
    sys42.tests.lambda-11"
    throw:Assert error, widgets misbehaved


List all lambda events
  p5.web.widgets.create:sys42.tests.widget-59
    events
      sys42.tests.lambda-10
        foo
      sys42.tests.lambda-11
        foo
      sys42.tests.lambda-12
  p5.web.widgets.lambda-events.vocabulary:~sys42.tests.lambda-
  p5.web.widgets.delete:sys42.tests.widget-59
  if:x:/@p5.web.widgets.lambda-events.vocabulary
    !=:node:@"p5.web.widgets.lambda-events.vocabulary
  dynamic:sys42.tests.lambda-10
  dynamic:sys42.tests.lambda-11"
    throw:Assert error, widgets misbehaved


Widget property before and after yields correct values
  p5.web.widgets.create-container:sys42.tests.widget-60
    widgets
      literal:sys42.tests.widget-61
      literal:sys42.tests.widget-62
  p5.web.widgets.property.get:sys42.tests.widget-61
    position
    before
  p5.web.widgets.property.get:sys42.tests.widget-62
    position
    after
  p5.web.widgets.delete:sys42.tests.widget-60
  if:x:/../*/p5.web.widgets.property.get/[0,1]
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-61
    position:int:0
    before:sys42.tests.widget-62"
    or:x:/../*/p5.web.widgets.property.get/[1,2]
      !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-62
    position:int:1
    after:sys42.tests.widget-61"
    throw:Assert error, widgets misbehaved


Widget property before and after yields correct values when there is nothing return
  p5.web.widgets.create-container:sys42.tests.widget-60
    widgets
      literal:sys42.tests.widget-61
      literal:sys42.tests.widget-62
  p5.web.widgets.property.get:sys42.tests.widget-61
    position
    after
  p5.web.widgets.property.get:sys42.tests.widget-62
    position
    before
  p5.web.widgets.delete:sys42.tests.widget-60
  if:x:/../*/p5.web.widgets.property.get/[0,1]
    !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-61
    position:int:0"
    or:x:/../*/p5.web.widgets.property.get/[1,2]
      !=:node:@"p5.web.widgets.property.get
  sys42.tests.widget-62
    position:int:1"
    throw:Assert error, widgets misbehaved


Recursive widget properties yields correct information
  p5.web.widgets.create-container:sys42.tests.widget-63
    class:foo
    widgets
      literal:test-1
      literal:test-2
        class:bar
      container:test-2-2
        class:foo-2
        widgets
          literal:test3
          literal:test-4
            class:bar-2
  p5.web.widgets.properties.get:sys42.tests.widget-63
    class
  p5.web.widgets.delete:sys42.tests.widget-63
  if:x:/@p5.web.widgets.properties.get
    !=:node:@"p5.web.widgets.properties.get
  sys42.tests.widget-63
    class:foo
  test-2
    class:bar
  test-2-2
    class:foo-2
  test-4
    class:bar-2"
    throw:Assert error, widgets misbehaved


Get parent widget yields correctly for single constant widget
  p5.web.widgets.create-container:sys42.tests.widget-64
    widgets
      literal:sys42.tests.widget-65
  p5.web.widgets.get-parent:sys42.tests.widget-65
  p5.web.widgets.delete:sys42.tests.widget-64
  if:x:/../*/p5.web.widgets.get-parent
    !=:node:@"p5.web.widgets.get-parent
  sys42.tests.widget-65
    container:sys42.tests.widget-64"
    throw:Assert error, widgets misbehaved


Get parent widget yields correctly for multiple constant widgets
  p5.web.widgets.create-container:sys42.tests.widget-64
    widgets
      literal:sys42.tests.widget-65
  p5.web.widgets.create-container:sys42.tests.widget-66
    widgets
      literal:sys42.tests.widget-67
  p5.web.widgets.get-parent
    sys42.tests.widget-65
    sys42.tests.widget-67
  p5.web.widgets.delete:sys42.tests.widget-64
  p5.web.widgets.delete:sys42.tests.widget-66
  if:x:/../*/p5.web.widgets.get-parent
    !=:node:@"p5.web.widgets.get-parent
  sys42.tests.widget-65
    container:sys42.tests.widget-64
  sys42.tests.widget-67
    container:sys42.tests.widget-66"
    throw:Assert error, widgets misbehaved


Get parent widget yields correctly for expressions leading to multiple widget IDs
  p5.web.widgets.create-container:sys42.tests.widget-64
    widgets
      literal:sys42.tests.widget-65
  p5.web.widgets.create-container:sys42.tests.widget-66
    widgets
      literal:sys42.tests.widget-67
  _widgets
    sys42.tests.widget-65
    sys42.tests.widget-67
  p5.web.widgets.get-parent:x:/@_widgets/*?name
  p5.web.widgets.delete:sys42.tests.widget-64
  p5.web.widgets.delete:sys42.tests.widget-66
  if:x:/../*/p5.web.widgets.get-parent
    !=:node:@"p5.web.widgets.get-parent
  sys42.tests.widget-65
    container:sys42.tests.widget-64
  sys42.tests.widget-67
    container:sys42.tests.widget-66"
    throw:Assert error, widgets misbehaved


Get children widget yields correctly for expressions leading to multiple widget IDs
  p5.web.widgets.create-container:sys42.tests.widget-64
    widgets
      literal:sys42.tests.widget-65
  p5.web.widgets.create-container:sys42.tests.widget-66
    widgets
      literal:sys42.tests.widget-67
  _widgets
    sys42.tests.widget-64
    sys42.tests.widget-66
  p5.web.widgets.get-children:x:/@_widgets/*?name
  p5.web.widgets.delete:sys42.tests.widget-64
  p5.web.widgets.delete:sys42.tests.widget-66
  if:x:/../*/p5.web.widgets.get-children
    !=:node:@"p5.web.widgets.get-children
  sys42.tests.widget-64
    literal:sys42.tests.widget-65
  sys42.tests.widget-66
    literal:sys42.tests.widget-67"
    throw:Assert error, widgets misbehaved


Get children widget yields correctly for multiple constant IDs
  p5.web.widgets.create-container:sys42.tests.widget-64
    widgets
      literal:sys42.tests.widget-65
  p5.web.widgets.create-container:sys42.tests.widget-66
    widgets
      literal:sys42.tests.widget-67
  p5.web.widgets.get-children
    sys42.tests.widget-64
    sys42.tests.widget-66
  p5.web.widgets.delete:sys42.tests.widget-64
  p5.web.widgets.delete:sys42.tests.widget-66
  if:x:/../*/p5.web.widgets.get-children
    !=:node:@"p5.web.widgets.get-children
  sys42.tests.widget-64
    literal:sys42.tests.widget-65
  sys42.tests.widget-66
    literal:sys42.tests.widget-67"
    throw:Assert error, widgets misbehaved


Widget find ancestor for single constant property
  p5.web.widgets.create-container:sys42.tests.widget-68
    class:foo
    widgets
      literal:sys42.tests.widget-69
  p5.web.widgets.find-ancestor:sys42.tests.widget-69
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-68
  if:x:/@p5.web.widgets.find-ancestor
    !=:node:@"p5.web.widgets.find-ancestor
  sys42.tests.widget-69
    container:sys42.tests.widget-68"
    throw:Assert error, widgets misbehaved


Widget find ancestor for single constant property does not stop on first match
  p5.web.widgets.create-container:sys42.tests.widget-68
    class:foo
    widgets
      container:sys42.tests.widget-69
        class:foo
        widgets
          literal:sys42.tests.widget-70
  p5.web.widgets.find-ancestor:sys42.tests.widget-70
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-68
  if:x:/@p5.web.widgets.find-ancestor
    !=:node:@"p5.web.widgets.find-ancestor
  sys42.tests.widget-70
    container:sys42.tests.widget-69
    container:sys42.tests.widget-68"
    throw:Assert error, widgets misbehaved


Widget find first ancestor for single constant property does stop on first match
  p5.web.widgets.create-container:sys42.tests.widget-68
    class:foo
    widgets
      container:sys42.tests.widget-69
        class:foo
        widgets
          literal:sys42.tests.widget-70
  p5.web.widgets.find-first-ancestor:sys42.tests.widget-70
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-68
  if:x:/@p5.web.widgets.find-first-ancestor
    !=:node:@"p5.web.widgets.find-first-ancestor
  sys42.tests.widget-70
    container:sys42.tests.widget-69"
    throw:Assert error, widgets misbehaved


Widget find ancestor like for single constant property
  p5.web.widgets.create-container:sys42.tests.widget-68
    class:foo-bar
    widgets
      container:sys42.tests.widget-69
        class:fo0
        widgets
          literal:sys42.tests.widget-70
  p5.web.widgets.find-ancestor-like:sys42.tests.widget-70
    class:foo
  p5.web.widgets.delete:sys42.tests.widget-68
  if:x:/@p5.web.widgets.find-ancestor-like
    !=:node:@"p5.web.widgets.find-ancestor-like
  sys42.tests.widget-70
    container:sys42.tests.widget-68"
    throw:Assert error, widgets misbehaved


List widgets like with constant static filter
  p5.web.widgets.create-container:sys42.tests.foo-1
    widgets
      container:sys42.tests.foo-2
        widgets
          literal:sys42.tests.foo-3
  p5.web.widgets.list-like:.foo-
  p5.web.widgets.delete:sys42.tests.foo-1
  if:x:/@p5.web.widgets.list-like
    !=:node:@"p5.web.widgets.list-like
  container:sys42.tests.foo-1
  container:sys42.tests.foo-2
  literal:sys42.tests.foo-3"
    throw:Assert error, widgets misbehaved


List widgets with constant multiple children filters
  p5.web.widgets.create-container:sys42.tests.foo-1
    widgets
      container:sys42.tests.foo-2
        widgets
          literal:sys42.tests.foo-3
  p5.web.widgets.list
    sys42.tests.foo-1
    sys42.tests.foo-2
    sys42.tests.foo-3
  p5.web.widgets.delete:sys42.tests.foo-1
  if:x:/@p5.web.widgets.list
    !=:node:@"p5.web.widgets.list
  container:sys42.tests.foo-1
  container:sys42.tests.foo-2
  literal:sys42.tests.foo-3"
    throw:Assert error, widgets misbehaved


Widgets exists with constant multiple children IDs
  p5.web.widgets.create-container:sys42.tests.foo-1
    widgets
      container:sys42.tests.foo-2
        widgets
          literal:sys42.tests.foo-3
  p5.web.widgets.exists
    sys42.tests.foo-2
    sys42.tests.foo-4
    sys42.tests.foo-1
    sys42.tests.foo-3
  p5.web.widgets.delete:sys42.tests.foo-1
  if:x:/@p5.web.widgets.exists
    !=:node:@"p5.web.widgets.exists
  sys42.tests.foo-2:bool:true
  sys42.tests.foo-4:bool:false
  sys42.tests.foo-1:bool:true
  sys42.tests.foo-3:bool:true"
    throw:Assert error, widgets misbehaved


Widgets exists with expression leading to multiple IDs
  p5.web.widgets.create-container:sys42.tests.foo-1
    widgets
      container:sys42.tests.foo-2
        widgets
          literal:sys42.tests.foo-3
  _widgets
    sys42.tests.foo-2
    sys42.tests.foo-4
    sys42.tests.foo-1
    sys42.tests.foo-3
  p5.web.widgets.exists:x:/@_widgets/*?name
  p5.web.widgets.delete:sys42.tests.foo-1
  if:x:/@p5.web.widgets.exists
    !=:node:@"p5.web.widgets.exists
  sys42.tests.foo-2:bool:true
  sys42.tests.foo-4:bool:false
  sys42.tests.foo-1:bool:true
  sys42.tests.foo-3:bool:true"
    throw:Assert error, widgets misbehaved
