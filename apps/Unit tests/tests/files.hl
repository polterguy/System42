
/*
 * Unit tests for everything related to files.
 */


Save and load single file
  p5.io.file.save:~/temp/foo.txt
    src:Foo bar
  p5.io.file.load:~/temp/foo.txt
  if:x:/@p5.io.file.load/*?value
    !=:Foo bar
    throw:Assert error, p5.io misbehaved


Delete single file
  p5.io.file.save:~/temp/bar.txt
    src:Bar was here
  p5.io.file.delete:~/temp/bar.txt
  if
    fetch:x:/0/0?value
      p5.io.file.exists:~/temp/bar.txt
    throw:Assert error, p5.io misbehaved


Deleting multiple files
  p5.io.file.save:~/temp/bar1.txt
    src:Bar1 was here
  p5.io.file.save:~/temp/bar2.txt
    src:Bar2 was here
  p5.io.file.delete:x:/../*/p5.io.file.save?value
  if
    fetch:x:/0/0?value
      p5.io.file.exists:~/temp/bar1.txt
    or
      fetch:x:/0/0?value
        p5.io.file.exists:~/temp/bar2.txt
    throw:Assert error, p5.io misbehaved


Copy a single file
  p5.io.file.save:~/temp/foo.txt
    src:Foo bar
  if
    fetch:x:/0/0?value
      p5.io.file.exists:~/temp/foo-copy.txt
    p5.io.file.delete:~/temp/foo-copy.txt
  p5.io.file.copy:~/temp/foo.txt
    dest:~/temp/foo-copy.txt
  p5.io.file.load:~/temp/foo-copy.txt
  if:x:/@p5.io.file.load/*?value
    !=:Foo bar
    throw:Assert error, p5.io misbehaved


Copy multiple files with expression
  p5.io.file.save:~/temp/foo-1.txt
    src:Foo bar 1
  p5.io.file.save:~/temp/foo-2.txt
    src:Foo bar 2
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/new/
    p5.io.folder.delete:~/temp/new/
  p5.io.folder.create:~/temp/new/
  p5.io.file.copy:x:/../*/p5.io.file.save?value
    dest:~/temp/new/
  p5.io.file.load:x:/../*/p5.io.file.save?value
  if:x:/@p5.io.file.load/0?value
    !=:Foo bar 1
    or:x:/@p5.io.file.load/1?value
      !=:Foo bar 2
    throw:Assert error, p5.io misbehaved


Move a single file
  p5.io.file.save:~/temp/foo-orig.txt
    src:Foo bar
  if
    fetch:x:/0/0?value
      p5.io.file.exists:~/temp/foo-dest.txt
    p5.io.file.delete:~/temp/foo-dest.txt
  p5.io.file.move:~/temp/foo-orig.txt
    dest:~/temp/foo-dest.txt
  p5.io.file.load:~/temp/foo-dest.txt
  if:x:/@p5.io.file.load/*?value
    !=:Foo bar
    or
      fetch:x:/0/0?value
        p5.io.file.exists:~/temp/foo-orig.txt
    throw:Assert error, p5.io misbehaved


Delete single read-only file throws
  _success:bool:false
  p5.io.file.save:~/temp/foo.txt
    src:Foo bar
  p5.io.file.read-only.set:~/temp/foo.txt
  try
    p5.io.file.delete:~/temp/foo.txt
  catch
    set:x:/@_success?value
      src:bool:true
  p5.io.file.read-only.delete:~/temp/foo.txt
  p5.io.file.delete:~/temp/foo.txt
  if:x:/@_success?value
    not
    throw:Assert error, p5.io misbehaved


Overwrite single read-only file throws
  _success:bool:false
  p5.io.file.save:~/temp/foo.txt
    src:Foo bar
  p5.io.file.read-only.set:~/temp/foo.txt
  try
    p5.io.file.save:~/temp/foo.txt
      src:Foo bar 2
  catch
    set:x:/@_success?value
      src:bool:true
  p5.io.file.read-only.delete:~/temp/foo.txt
  p5.io.file.delete:~/temp/foo.txt
  if:x:/@_success?value
    not
    throw:Assert error, p5.io misbehaved


Retrieve single read-only file flag
  p5.io.file.save:~/temp/foo.txt
    src:Foo bar
  p5.io.file.read-only.set:~/temp/foo.txt
  p5.io.file.read-only.get:~/temp/foo.txt
  p5.io.file.read-only.delete:~/temp/foo.txt
  p5.io.file.delete:~/temp/foo.txt
  if:x:/@p5.io.file.read-only.get/*?value
    not
    throw:Assert error, p5.io misbehaved


Retrieve multiple read-only file values
  p5.io.file.save:~/temp/foo-1.txt
    src:Foo bar
  p5.io.file.save:~/temp/foo-2.txt
    src:Foo bar
  p5.io.file.read-only.set:x:/../*/p5.io.file.save?value
  p5.io.file.read-only.get:x:/../*/p5.io.file.save?value
  p5.io.file.read-only.delete:x:/../*/p5.io.file.save?value
  p5.io.file.delete:x:/../*/p5.io.file.save?value
  if:x:/@p5.io.file.read-only.get/0?value
    not
    or:x:/@p5.io.file.read-only.get/1?value
      not
    throw:Assert error, p5.io misbehaved


Retrieve multiple read-only file values where file is not read-only
  p5.io.file.save:~/temp/foo-1.txt
    src:Foo bar
  p5.io.file.save:~/temp/foo-2.txt
    src:Foo bar
  p5.io.file.read-only.set:x:/../*/p5.io.file.save?value
  p5.io.file.read-only.delete:x:/../*/p5.io.file.save?value
  p5.io.file.read-only.get:x:/../*/p5.io.file.save?value
  p5.io.file.delete:x:/../*/p5.io.file.save?value
  if:x:/@p5.io.file.read-only.get/0?value
    or:x:/@p5.io.file.read-only.get/1?value
    throw:Assert error, p5.io misbehaved


Retrieve single file length
  p5.io.file.save:~/temp/foo.txt
    src:Foo bar
  p5.io.file.get-length:~/temp/foo.txt
  if:x:/@p5.io.file.get-length/0?value
    !=:long:7
    throw:Assert error, p5.io misbehaved
