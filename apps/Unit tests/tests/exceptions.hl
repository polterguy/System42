
/*
 * Unit tests for [try], [catch], [finally] and [throw].
 */


Empty try and catch is tolerated
  try
  catch


Empty try and finally is tolerated
  try
  finally


Catch never executed if no throw
  _result:success
  try
  catch
    set:x:/@_result?value
      src:failure
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved


Finally is executed if no throw
  _result:failure
  try
  finally
    set:x:/@_result?value
      src:success
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved


Throw and catch exception, verifying message
  _result
  try
    throw:success
  catch
    set:x:/@_result?value
      src:x:/@message?value
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved


Throw and catch exception, verifying type
  _result
  try
    throw:foo
  catch
    set:x:/@_result?value
      src:x:/@type?value
  if:x:/@_result?value
    !=:p5.exp.exceptions.LambdaException
    throw:Assert error, exceptions misbehaved


Throw and catch exception, verifying stacktrace exists
  _result
  try
    throw:foo
  catch
    set:x:/@_result?value
      src:x:/@stack-trace?value
  if:x:/@_result?value
    not
    throw:Assert error, exceptions misbehaved


Throw and catch exception, verifying finally executes
  _result
  try
    throw:success
  catch
  finally
    set:x:/@_result?value
      src:success
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved


Throw on try but no catch-finally
  _result
  try
    try
  catch
    set:x:/@_result?value
      src:success
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved


Throw on wrong catch-finally order
  _result
  try
    try
    finally
    catch
  catch
    set:x:/@_result?value
      src:success
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved


Throw on catch but no try
  _result
  try
    catch
  catch
    set:x:/@_result?value
      src:success
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved


Throw on finally but no try
  _result
  try
    finally
  catch
    set:x:/@_result?value
      src:success
  if:x:/@_result?value
    !=:success
    throw:Assert error, exceptions misbehaved
