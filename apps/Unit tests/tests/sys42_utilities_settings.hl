
/*
 * Unit tests for the [sys42.utilities.xxx-setting] Active Events.
 */


Set simple string setting value
  sys42.settings.set:foo-bar
    app:sys42.unit-tests
    src:Foo Bar
  sys42.settings.get:foo-bar
    app:sys42.unit-tests
  if:x:/@sys42.settings.get/*/foo-bar?value
    !=:Foo Bar
    throw:Assert error, [sys42.settings.set] misbehaved


Set graph object setting value
  sys42.settings.set:foo-bar
    app:sys42.unit-tests
    src
      name:Foo
      value:Bar
  sys42.settings.get:foo-bar
    app:sys42.unit-tests
  if:x:/@sys42.settings.get/*/foo-bar/*/name?value
    !=:Foo
    or:x:/@sys42.settings.get/*/foo-bar/*/value?value
      !=:Bar
    or:x:/@sys42.settings.get/*?value
    throw:Assert error, [sys42.settings.set] misbehaved


Delete simple string setting value
  sys42.settings.set:foo-bar
    app:sys42.unit-tests
    src:Foo Bar
  sys42.settings.set:foo-bar
    app:sys42.unit-tests
  if:x:/@sys42.settings.get/*/foo-bar
    throw:Assert error, [sys42.settings.set] misbehaved


Update graph object setting value
  sys42.settings.set:foo-bar
    app:sys42.unit-tests
    src
      name:Foo
      value:Bar
  sys42.settings.set:foo-bar
    app:sys42.unit-tests
    src
      name:Foo2
      value:Bar2
  sys42.settings.get:foo-bar
    app:sys42.unit-tests
  if:x:/@sys42.settings.get
    !=:node:@"sys42.settings.get
  foo-bar
    name:Foo2
    value:Bar2"
    throw:Assert error, [sys42.settings.set] misbehaved


List settings for app
  sys42.settings.set:foo-bar1
    app:sys42.unit-tests-list
    src:Foo1
  sys42.settings.set:foo-bar2
    app:sys42.unit-tests-list
    src:Foo2
  sys42.settings.list
    app:sys42.unit-tests-list
  if:x:/@sys42.settings.list
    !=:node:@"sys42.settings.list
  foo-bar1:Foo1
  foo-bar2:Foo2"
    throw:Assert error, [sys42.settings.list] misbehaved
