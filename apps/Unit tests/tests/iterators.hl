
/*
 * Unit tests for expression iterators.
 */


Reference iterator
  _data:node:
  set:x:/@_data/#?name
    src:foo
  set:x:/@_data/#?value
    src:bar
  if:x:/@_data/#?name
    !=:foo
    or:x:/@_data/#?value
      !=:bar
    throw:Assert error, iterator misbehaved


Range iterator
  _out
  _data
    foo1
    foo2
    foo3
    foo4
  add:x:/@_out
    src:x:/@_data/*/[1,3]?name
  if:x:/@_out
    !=:node:@"_out
  foo2
  foo3"
    throw:Assert error, iterator misbehaved


Descendants iterator
  _out
  _data
    foo1
      foo2
        foo3
      foo4
        foo5
    foo6
      foo7
  add:x:/@_out
    src:x:/@_data/**?name
  if:x:/-3
    !=:node:@"_out
  foo1
  foo2
  foo3
  foo4
  foo5
  foo6
  foo7"
    throw:Assert error, iterator misbehaved


Children iterator
  _out
  _data
    foo1
      foo2
        foo3
    foo6
  add:x:/@_out
    src:x:/@_data/*?name
  if:x:/@_out
    !=:node:@"_out
  foo1
  foo6"
    throw:Assert error, iterator misbehaved


Modulo iterator
  _out
  _data
    foo1
    foo2
    foo3
    foo4
  add:x:/@_out
    src:x:/@_data/*/%2?name
  if:x:/@_out
    !=:node:@"_out
  foo2
  foo4"
    throw:Assert error, iterator misbehaved


Named iterator
  _data
    foo1
    foo2
    foo3
    foo4
  set:x:/@_data?value
    src:x:/@_data/*/foo2?name
  if:x:/@_data?value
    !=:foo2
    throw:Assert error, iterator misbehaved


Named ancestor iterator
  _data
    foo1
      foo2
        foo3
          foo4
  set:x:/@_data?value
    src:x:/@_data/**/foo4/..foo1?name
  if:x:/@_data?value
    !=:foo1
    throw:Assert error, iterator misbehaved


Numbered child iterator
  _data
    foo1
    foo2
    foo3
    foo4
  set:x:/@_data?value
    src:x:/@_data/2?name
  if:x:/@_data?value
    !=:foo3
    throw:Assert error, iterator misbehaved


Parent iterator
  _data
    foo1
      foo2
        foo3
          foo4
  set:x:/@_data?value
    src:x:/@_data/**/foo4/./.?name
  if:x:/@_data?value
    !=:foo2
    throw:Assert error, iterator misbehaved


Left shift iterator
  _out
  _data
    foo1
      foo2
        foo3
          foo4
        foo5
          foo6
      foo7
  add:x:/@_out
    src:x:/@_data/**/foo7/<|/@_data/**/foo7/</<|/@_data/**/foo7/</</<|/@_data/**/foo7/</</</<?name
  if:x:/@_out
    !=:node:@"_out
  foo6
  foo5
  foo4
  foo3"
    throw:Assert error, iterator misbehaved


Right shift iterator
  _out
  _data
    foo1
      foo2
        foo3
          foo4
        foo5
          foo6
      foo7
  add:x:/@_out
    src:x:/@_data/0|/@_data/0/>|/@_data/0/>/>|/@_data/0/>/>/>|/@_data/0/>/>/>/>|/@_data/0/>/>/>/>/>|/@_data/0/>/>/>/>/>/>?name
  if:x:/-3
    !=:node:@"_out
  foo1
  foo2
  foo3
  foo4
  foo5
  foo6
  foo7"
    throw:Assert error, iterator misbehaved


Elder and younger sibling iterators
  _out
  _data
    foo1
    foo2
    foo3
    foo4
    foo5
    foo6
    foo7
  add:x:/@_out
    src:x:/@_data/0|/@_data/0/+|/@_data/3/-|/@_data/6/+4|/@_data/0/-3?name
  if:x:/@_out
    !=:node:@"_out
  foo1
  foo2
  foo3
  foo4
  foo5"
    throw:Assert error, iterator misbehaved


Value iterator with constant string equality
  _data
    foo1:bar1
    foo2:bar2
    foo3:bar3
  set:x:/@_data?value
    src:x:/@_data/*/=bar2?name
  if:x:/@_data?value
    !=:foo2
    throw:Assert error, iterator misbehaved


Value iterator with constant string like equality
  _data
    foo1:bar1
    foo2:bar2
    foo3:bar3
  set:x:/@_data?value
    src:x:/@_data/*/=~r2?name
  if:x:/@_data?value
    !=:foo2
    throw:Assert error, iterator misbehaved


Value iterator with constant string like value being empty
  _out
  _data
    foo1
    foo2:
    foo3:bar3
  add:x:/@_out
    src:x:/@_data/*/=~?name
  if:x:/-3
    !=:node:@"_out
  foo1
  foo2"
    throw:Assert error, iterator misbehaved


Value iterator with escaped value content
  _data
    foo1:foo1
    foo2:~
    foo3:bar3
  set:x:/@_data?value
    src:x:/@_data/*/=\~?name
  if:x:/@_data?value
    !=:foo2
    throw:Assert error, iterator misbehaved


Name iterator with like comparison
  _data
    foo1:bar1
    foo2:bar2
    foo3:bar3
  set:x:/@_data?value
    src:x:/@_data/*/~oo2?value
  if:x:/@_data?value
    !=:bar2
    throw:Assert error, iterator misbehaved


Name iterator with empty name
  _data
    foo1:bar1
    :bar2
    foo3:bar3
  set:x:/@_data?value
    src:x:/@_data/*//?value
  if:x:/@_data?value
    !=:bar2
    throw:Assert error, iterator misbehaved


Name iterator with escaped name
  _data
    foo1:bar1
    ~:bar2
    foo3:bar3
  set:x:/@_data?value
    src:x:/@_data/*/\~?value
  if:x:/@_data?value
    !=:bar2
    throw:Assert error, iterator misbehaved


Distinct name iterator
  _out
  _data
    foo1:su
    foo1:error
    foo2:cc
    foo2:error
    foo3:ess
  add:x:/@_out
    src:x:/@_data/*/$?value
  if:x:/@_out
    !=:node:@"_out
  :su
  :cc
  :ess"
    throw:Assert error, iterator misbehaved


Distinct value iterator
  _out
  _data
    su:foo1
    error:foo1
    cc:foo2
    error:foo2
    ess:int:4
  add:x:/@_out
    src:x:/@_data/*/=$?name
  if:x:/@_out
    !=:node:@"_out
  su
  cc
  ess"
    throw:Assert error, iterator misbehaved


Name iterator in double quotes
  _data
    /xx/:success
  set:x:/@_data?value
    src:x:@"/@_data/*/""/xx/""?value"
  if:x:/@_data?value
    !=:success
    throw:Assert error, iterator misbehaved
