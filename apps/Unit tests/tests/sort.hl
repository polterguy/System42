
/*
 * Unit tests for [sort] and [sort-desc].
 */


String sort with lambda
  _data
    foo1
      name:brynjar
    foo2
      name:kalvik
    foo3
      name:abba
  sort:x:/@_data/*
    if:x:/@_lhs/#/*?value
      <:x:/@_rhs/#/*?value
      return:int:-1
    else-if:x:/@_lhs/#/*?value
      >:x:/@_rhs/#/*?value
      return:int:1
  if:x:/@sort
    !=:node:@"sort
  foo3
    name:abba
  foo1
    name:brynjar
  foo2
    name:kalvik"
    throw:Assert error, [sort] misbehaved


Integer sort with lambda
  _data
    foo1:int:60
    foo2:int:57
    foo3:int:88
  sort:x:/@_data/*
    if:x:/@_lhs/#?value
      <:x:/@_rhs/#?value
      return:int:-1
    else-if:x:/@_lhs/#?value
      >:x:/@_rhs/#?value
      return:int:1
  if:x:/@sort
    !=:node:@"sort
  foo2:int:57
  foo1:int:60
  foo3:int:88"
    throw:Assert error, [sort] misbehaved


Sort without lambda
  _data
    brynjar
    kalvik
    abba
  sort:x:/@_data/*
  if:x:/@sort
    !=:node:@"sort
  abba
  brynjar
  kalvik"
    throw:Assert error, [sort] misbehaved


Sort descending without lambda
  _data
    brynjar
    kalvik
    abba
  sort-desc:x:/@_data/*
  if:x:/@sort-desc
    !=:node:@"sort-desc
  kalvik
  brynjar
  abba"
    throw:Assert error, [sort] misbehaved
