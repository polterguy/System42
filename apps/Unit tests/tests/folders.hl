
/*
 * Unit tests for everything related to folders.
 */


Create a single folder
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo/
    p5.io.folder.delete:~/temp/foo/
  p5.io.folder.create:~/temp/foo/
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo/
    not
    throw:Assert error, p5.io misbehaved


Creates multiple folders with expression
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo1/
    p5.io.folder.delete:~/temp/foo1/
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo2/
    p5.io.folder.delete:~/temp/foo2/
  _folders
    ~/temp/foo1/
    ~/temp/foo2/
  p5.io.folder.create:x:/@_folders/*?name
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo1/
    not
    or
      fetch:x:/0/0?value
        p5.io.folder.exists:~/temp/foo2/
      not
    throw:Assert error, p5.io misbehaved


Copy folder with sub-folders
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo-bar/
    p5.io.folder.delete:~/temp/foo-bar/
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo-bar-2/
    p5.io.folder.delete:~/temp/foo-bar-2/
  p5.io.folder.create:~/temp/foo-bar/
  p5.io.folder.create:~/temp/foo-bar/inner/
  p5.io.file.save:~/temp/foo-bar/inner/foo.txt
    src:Foo was here
  p5.io.folder.copy:~/temp/foo-bar/
    dest:~/temp/foo-bar-2/
  p5.io.file.load:~/temp/foo-bar-2/inner/foo.txt
  if:x:/@p5.io.file.load/*?value
    !=:Foo was here
    throw:Assert error, p5.io misbehaved


List folders
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo/
    p5.io.folder.delete:~/temp/foo/
  _folders
    ~/temp/foo/
    ~/temp/foo/bar1/
    ~/temp/foo/bar2/
  p5.io.folder.create:x:/@_folders/*?name
  p5.io.folder.list-folders:~/temp/foo/
  if:x:/@p5.io.folder.list-folders/*/~bar1
    not
    or:x:/@p5.io.folder.list-folders/*/~bar2
      not
    or:x:/@p5.io.folder.list-folders/*?count
      !=:int:2
    throw:Assert error, p5.io misbehaved


List folders with expression
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/foo/
    p5.io.folder.delete:~/temp/foo/
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/bar/
    p5.io.folder.delete:~/temp/bar/
  _folders
    ~/temp/foo/
    ~/temp/foo/bar1/
    ~/temp/foo/bar2/
    ~/temp/bar/
    ~/temp/bar/bar3/
    ~/temp/bar/bar4/
  p5.io.folder.create:x:/@_folders/*?name
  _folders
    no1:~/temp/foo/
    no1:~/temp/bar/
  p5.io.folder.list-folders:x:/@_folders/*?value
  if:x:/@p5.io.folder.list-folders/*/~bar1
    not
    or:x:/@p5.io.folder.list-folders/*/~bar2
      not
    or:x:/@p5.io.folder.list-folders/*/~bar3
      not
    or:x:/@p5.io.folder.list-folders/*/~bar4
      not
    or:x:/@p5.io.folder.list-folders/*?count
      !=:int:4
    throw:Assert error, p5.io misbehaved


List folders throws on bad path
  _success:bool:false
  try
    p5.io.folder.list-folders:~/temp
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, p5.io misbehaved


List files
  if
    fetch:x:/0/0?value
      p5.io.folder.exists:~/temp/p5.io.folder.list-files/
    p5.io.folder.delete:~/temp/p5.io.folder.list-files/
  p5.io.folder.create:~/temp/p5.io.folder.list-files/
  p5.io.file.save:~/temp/p5.io.folder.list-files/foo-orig.txt
    src:Foo bar
  p5.io.folder.list-files:~/temp/p5.io.folder.list-files/
  if:x:/-/*?count
    !=:int:1
    or:x:/./-/*/~foo-orig.txt
      not
    throw:@"Assert error, file wasnt't correctly renamed!"


List files throws on bad path
  _success:bool:false
  try
    p5.io.folder.list-folders:~/temp
  catch
    set:x:/@_success?value
      src:bool:true
  if:x:/@_success?value
    not
    throw:Assert error, p5.io misbehaved
