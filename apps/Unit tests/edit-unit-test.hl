
/*
  * Retrieving parent's parent widget (which should be the "tr" HTML row)
  */
p5.web.widgets.get-parent:x:/../*/_widget?value
p5.web.widgets.get-parent:x:/-/*/*?value

/*
 * Loading unit test file, making sure we do NOT convert to p5.lambda, to
 * preserve comments
 */
p5.io.file.load:x:/../*/_file?value
  convert:false

/*
 * Then creating a new "tr" row, after that one, which contains only one column, containing the content
 * of our currently edited test file
 */
eval-x:x:/+/*|/+|/+/**(/~codemirror|/_file)|/+/*/*/*/*/*/_value
p5.web.widgets.create-container:{0}_editor
  :x:/../*/_widget?value
  after:x:/../*/p5.web.widgets.get-parent/[1,2]/*/*?value
  element:tr
  widgets
    td
      colspan:3
      style:"border-top:none;"
      widgets

        /*
         * Our CodeMirror custom editor widget
         */
        sys42.widgets.codemirror:{0}_codemirror
          :x:/../*/_widget?value
          auto-focus:true
          height:700px
          value:x:/../*/p5.io.file.load/*?value
        container
          class:btn-group col-xs-3 col-xs-push-9
          widgets

            /*
             * The "Save" button for edited unit test
             */
            literal
              element:button
              style:"width:33%;"
              class:btn btn-default btn-attach-top
              innerValue:Save
              onclick

                /*
                 * Retrieving value from CodeMirror "textarea", and saving to specified test file.
                 * [_file] and [_codemirror] is forward evaluated before creation of widget
                 */
                _file:x:/../*/_file?value
                _codemirror:{0}_codemirror
                  :x:/../*/_widget?value
                p5.web.widgets.property.get:x:/-?value
                  value
                p5.io.file.save:x:/../*/_file?value
                  src:x:/../*/p5.web.widgets.property.get/*/*?value

                /*
                 * Feedback to user
                 */
                sys42.windows.info-tip:File was saved

            /*
             * The "Delete" button for edited unit test
             */
            literal
              element:button
              style:"width:33%;"
              class:btn btn-default btn-attach-top
              innerValue:Delete
              onclick

                /*
                 * Deletes the specified test
                 */
                sys42.windows.confirm
                  header:Confirm deletion!
                  body:Are you sure you wish to delete this file?
                  .onok
                    _file:x:/../*/_file?value
                    p5.io.file.delete:x:/../*/_file?value
                    sys42.windows.info-tip:File was deleted
                    p5.web.widgets.delete:unit-test-bottom-toolbar
                    p5.web.widgets.delete:unit-test-datagrid
                    sys42.utilities.execute-lambda-file:@TESTS/launch.hl

            /*
             * The "Close" button for edited unit test, such that user
             * can stop editing test
             */
            literal
              element:button
              style:"width:33%;"
              class:btn btn-default btn-attach-top
              innerValue:Close
              onclick

                /*
                 * Deleting entire "editor HTML row" ("tr" element wrapping CodeMirror)
                 */
                p5.web.widgets.find-ancestor:x:/../*/_event?value
                  element:tr
                p5.web.widgets.delete:x:/-/*/*?value

