
/*
 * Creates a new unit test file in system.
 * Simply shows a modal window, asking user to provide name for test file,
 * and if user clicks "OK", creates a new default file for user.
 */
sys42.windows.modal
  header:Name of unit test file
  widgets
    container:test-filename-wrp
      class:form-group
      widgets
        label
          for:test-filename
          innerValue:Name of test
        input:test-filename
          type:text
          class:form-control
          placeholder:Name of file ...
  buttons
    button:test-create-btn
      class:btn btn-default
      innerValue:Create
      onclick

        /*
         * Getting name of new test file, and verifying it's not empty
         */
        p5.web.widgets.property.get:test-filename
          value
        if:x:/../*/p5.web.widgets.property.get/*/*?value
          =:
          p5.web.widgets.property.set:test-filename-wrp
            class:form-group has-error
          p5.web.send-javascript:@"$('#test-filename').focus().select();"
          return

        /*
         * Checking if user supplied the name without a ".hl" extension, and if so, we add it
         */
        p5.string.split:x:/../*/p5.web.widgets.property.get/*/*?value
          =:.
        if:x:/-/*?count
          =:int:1
          set:x:/../*/p5.web.widgets.property.get/*/*?value
            src:{0}.hl
              :x:/../*/p5.web.widgets.property.get/*/*?value

        /*
         * Checking if user supplied a slash in the filename, and if so, alert him that this is an error
         */
        p5.string.split:x:/../*/p5.web.widgets.property.get/*/*?value
          =:/
        if:x:/-/*?count
          >:int:1
          p5.web.widgets.property.set:test-filename-wrp
            class:form-group has-error
          p5.web.send-javascript:@"$('#test-filename').focus().select();"
        else

          /*
           * Filename of test file is OK, creating a default test file
           */
          p5.io.file.save:{0}{1}
            :@TESTS/tests/
            :x:/../*/p5.web.widgets.property.get/*/*?value
            src:@"/*
 * Contains unit tests for testing [whatever] in system
 */

/*
 * Tests that [foo] behaves correctly
 */
foo
  _some-test-code-goes-here
  if:bool:false
    throw:@""Assert error, expected [something], got [something-else]!""
"

          /*
           * Making sure we close our modal window
           */
          sys42.windows.modal.cancel

          /*
           * Deleting widgets and re-creating datagrid (and other widgets) by invoking "self"
           */
          p5.web.widgets.delete:unit-test-bottom-toolbar
          p5.web.widgets.delete:unit-test-datagrid
          sys42.utilities.execute-lambda-file:@TESTS/launch.hl

          /*
           * Making sure our newly created test is edited
           */
          p5.io.unroll-path.@TESTS
          p5.web.widgets.find
            _file-button:{0}/tests/{1}
              :x:/././-?value
              :x:/../*/p5.web.widgets.property.get/*/*?value
          p5.web.widgets.ajax-events.raise:x:/-/*/*?value
            onclick

