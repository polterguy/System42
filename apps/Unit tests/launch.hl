
/*
 * Allows the user to inspect, edit, and evaluate unit tests in the system.
 * All unit tests can be found in the "/tests/" folder.
 */


/*
 * Verifies user is root first, since only root can run unit tests
 */
p5.auth.misc.whoami
if:x:/-/*/role?value
  !=:root
  throw:Only root user can run unit tests


/*
 * Loads the default template.
 */
sys42.cms.load-template


/*
 * List all files in "/tests/" directory.
 * Each file is considered a "single test collection for specific concept".
 */
p5.io.folder.list-files:@TESTS/tests/
  filter:.hl


/*
 * Callback invoked while [apply]'ing innerValue of unit test name,
 * such that we can remove the directory path and ".hl" extension.
 */
_eval-name
  p5.string.split:x:/../*/_dn/#?name
    =:/
    =:.
  eval-x:x:/+/*/*
  insert-before:x:/../0
    src
      innerValue:x:/../*/p5.string.split/0/-2?name


/*
 * Callback invoked while [apply]'ing innerValue of unit test content.
 */
_eval-content
  try
    p5.io.file.load:x:/../*/_dn/#?name
    p5.string.join:x:/-/*/*?name
      sep:"\r\n</li><li>"
    eval-x:x:/..try/*/return/*
    return
      innerValue:<pre><ol><li>{0}</li></ol></pre>
        :x:/..try/*/p5.string.join?value
  catch
    return:Syntax error in Hyperlambda file
  return
    innerValue:Foo bar


/*
 * Applying unit tests files to table widget
 */
apply:x:/../*/p5.web.widgets.create-container/=unit-test-datagrid/*/widgets/*/tbody/*/widgets
  src:x:/../*/p5.io.folder.list-files/*
  template
    tr

      /*
       * Used for retrieving unit test file row, to signal success or failure,
       * through red or green color on row as a whole
       */
      {_file-row}:x:?name
      widgets

        /*
         * Unit test filename.
         * If the user clicks the filename, then the Hyperlambda/CodeMirror editor is launched, allowing
         * the user to edit the specified test.
         */
        td
          widgets
            a
              {@eval}:x:/../*/_eval-name
              href:#
              title:Click to edit or view unit test file
              {_file-button}:x:?name
              onclick

                /*
                 * Checking  if editor is already loaded, and if so, deleting existing editing widget (entire "tr" element).
                 * Returns early if editor exists.
                 */
                if
                  fetch:x:/0/0?value
                    p5.web.widgets.exists:{0}_editor
                      :x:/../*/_event?value

                  /*
                   * Deleting existing editor, since file is already edited, before returning early.
                   */
                  p5.web.widgets.delete:{0}_editor
                    :x:/../*/_event?value
                  return

                /*
                 * Invoking the Hyperlambda file responsible for creating our CodeMirror 
                 * editor (in a "tr" row beneath test row)
                 */
                add:x:/+
                  src:"_widget:{0}"
                    :x:/../*/_event?value
                sys42.utilities.execute-lambda-file:@TESTS/edit-unit-test.hl
                  {_file}:x:?name

        /*
         * Result text from evaluation of file. Contains either one or more bugs, or the names of all
         * unit tests that was evaluated successfully. Value depends upon whether or not evaluation of 
         * unit tests was a success or not.
         */
        td
          style:"width:60%;"
          {@eval}:x:/../*/_eval-content
          {_file-result}:x:?name

        /*
         * Button that allows evaluation of a single unit test file.
         * Notice that you cannot run a single unit test, you can run a single test file though.
         */
        td
          widgets
            button
              class:btn btn-default form-control
              innerValue:Evaluate
              onclick

                /*
                 * Evaluates the unit test file, which may contain multiple unit tests. 
                 * [_file] is filename of unit test file, created during [apply].
                 */
                {_file}:x:?name
                add:x:/+
                  src:x:@"/../*/"":regex:/^_/"""
                sys42.utilities.execute-lambda-file:@TESTS/single-test-button-clicked.hl


/*
 * Now, after the above [apply], and other sanity checks, 
 * we can finally create the widget that displays all unit tests in the system.
 * This creates an HTML table, with one row for each test file.
 */
p5.web.widgets.create-container:unit-test-datagrid
  parent:content
  element:table
  class:table
  events


  /*
   * Widgets that are being databound above to show table containing all unit test
   * files in system
   */
  widgets
    thead
      widgets
        tr
          widgets
            th
              innerValue:File
            th
              innerValue:Tests
            th
              class:text-right
              widgets
                button
                  class:btn btn-primary form-control
                  innerValue:Evaluate all
                  onclick

                    /*
                     * Evaluating all unit tests in one go
                     */
                    add:x:/+
                      src:x:/../*/_event
                    sys42.utilities.execute-lambda-file:@TESTS/evaluate-all.hl
    tbody
      widgets


/*
 * Creates widget at the bottom, containing button row with "+", "Clear" and "Coverage" buttons.
 */
p5.web.widgets.create-container:unit-test-bottom-toolbar
  parent:content
  class:col-xs-6 col-xs-push-6 col-md-4 col-md-push-8 col-lg-3 col-lg-push-9 text-right prepend-top
  widgets


    /*
     * Shows the coverage for the test suite button
     */
    button
      class:btn btn-default col-xs-4
      innerValue:Coverage
      title:Shows the coverage of your unit tests
      onclick

        /*
         * Simply evaluates the "show-coverage.hl" file
         */
        sys42.utilities.execute-lambda-file:@TESTS/show-coverage.hl
