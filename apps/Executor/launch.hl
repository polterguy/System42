
/*
 * Allows the user to execute p5 lambda given through a 
 * syntax highlighted textarea widget, seeing the results 
 * in another textarea result widget. Relies upon CodeMirror.
 */


/*
 * Verifying user is authorized to access this application
 */
p5.auth.misc.whoami
if:x:/-/*/role?value
  !=:root
  throw:@"Sorry, this page is only accessible to ""root acounts"""


/*
 * Loads the default template.
 */
sys42.cms.load-template


/*
 * Invoking custom control event, for later to transform its results, into a [p5.web.widgets.create-container] invocation.
 */
sys42.widgets.codemirror-executor


/*
 * Making sure the [container] returned from invoking custom control event, is transformed into [p5.web.widgets.create-container],
 * with the proper [parent].
 */
set:x:/../*/sys42.widgets.codemirror-executor/0?name
  src:p5.web.widgets.create-container
add:x:/../*/sys42.widgets.codemirror-executor/0
  src
    parent:content


/*
 * Then evaluating the results of our [sys42.widgets.codemirror-executor], which is now a [p5.web.widgets.create-container] invocation
 */
eval:x:/../*/sys42.widgets.codemirror-executor

