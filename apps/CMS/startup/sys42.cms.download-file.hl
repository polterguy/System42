
/*
 * Responsible for creating the Active Event that allows you to download a file from the server.
 *
 * Pass in optionally [_file-type] and [filename] to override type and filename.
 */
create-event:sys42.cms.download-file

  /*
   * Verifying file exists, and that user is legally allowed to read it.
   *
   * Notice, this invocation will throw an exception if user is not legally allowed to read the file.
   */
  file-exists:x:/../*/_arg?value
  if:x:/@file-exists/*?value
    not
    throw:Couldn't find file.

  /*
   * Puts the given filename and path into session, and simply reloads location, since actual download is
   * done in [p5.web.load-ui].
   */
  p5.web.session.set:sys42.cms.download-filename
    src:x:/../*/_arg?value
  if:x:/../*/_file-type?value
    p5.web.session.set:sys42.cms.download-filetype
      src:x:/../*/_file-type?value
  if:x:/../*/filename?value
    p5.web.session.set:sys42.cms.download-filename-return
      src:x:/../*/filename?value

  /*
   * Reloading location, letting [p5.web.load-ui] do the "heavy lifting".
   */
  p5.web.reload-location
