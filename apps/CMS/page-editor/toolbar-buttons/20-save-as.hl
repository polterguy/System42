
/*
 * Save as toolbar button for page editor.
 */
button
  innerValue:@"<span class=""glyphicon glyphicon-floppy-save""></span> Save as ..."
  class:btn btn-default diffuse btn-attach-top
  onclick

    /*
     * Getting name and URL of page, such that we can use it as basis for new name and URL.
     */
    sys42.cms.get-edited-page-id
    p5.data.select:x:@"/*/*/p5.page/""={0}"""
      :x:/@sys42.cms.get-edited-page-id?value

    /*
     * Forward evaluating expressions inside of [_widgets] below
     */
    eval-x:x:/+/*/*/*/*/*/value|/+/*/.onok/*/_old-page
    sys42.windows.modal
      header:Supply name and URL of new page
      widgets
        div:new-page-name-wrp
          class:form-group
          widgets
            label
              for:new-page-name
              class:control-label
              innerValue:Page name
            input:new-page-name
              type:text
              class:form-control
              placeholder:Name of page ...
              value:{0} - copy
                :x:/@p5.data.select/*/*/name?value
        div:new-page-url-wrp
          class:form-group
          widgets
            label
              for:new-page-url
              class:control-label
              innerValue:Page URL
            input:new-page-url
              type:url
              class:form-control
              placeholder:URL of page ...
              value:{0}-copy
                :x:/@p5.data.select/*?value
      .oncancel

        /*
         * Making sure we set focus to select page dropdown if user closes dialogue, without pressing "OK".
         */
        p5.web.send-javascript:@"$('#cms-select-page').focus().select();"

      .onok

        /*
         * Will contain the database id of old page.
         */
        _old-page:x:/@p5.data.select/*?value

        /*
         * Getting the new page's name and URL.
         */
        p5.web.widgets.property.get:new-page-name
          value
        p5.web.widgets.property.get:new-page-url
          value

        /*
         * Making sure supplied name does not exist from before.
         */
        p5.data.select:x:@"/*/*/p5.page/""={0}"""
          :x:/@p5.web.widgets.property.get/*/*?value
        if:x:/@p5.data.select/*?count
          >:int:0

          /*
           * Page exists from before, alerting user, and returning false early, to make sure window is not closed.
           */
          sys42.utilities.add-css-classes:new-page-url-wrp
            class:has-error
          p5.web.send-javascript:@"$('#new-page-url').focus().select();"
          return:bool:false

        /*
         * Getting page data, and saving as a new object in database
         */
        sys42.cms.get-specialized-editor-data

        /*
         * Then making sure we select old page, and replace [name] and [url] (which is ID of database object), 
         * in addition to new data, before saving page as a new [p5.page] object in database.
         */
        p5.data.select:x:@"/*/*/p5.page/""={0}"""
          :x:/@_old-page?value

        /*
         * Changing id (URL of page) to new URL, in addition to changing name of page.
         */
        set:x:/@p5.data.select/*?value
          src:x:/@p5.web.widgets.property.get/*/*?value
        set:x:/@p5.data.select/*/*/name?value
          src:x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value

        /*
         * Then removing all items from [p5.data.select] that also exists in [sys42.cms.get-specialized-editor-data].
         */
        for-each:x:/@p5.data.select/*/*
          if:x:@"/@sys42.cms.get-specialized-editor-data/*/""{0}"""
            :x:/@_dp/#?name
            set:x:/@_dp/#

        /*
         * Then we can append all items from [p5.data.select] and [sys42.cms.get-specialized-editor-data] into [p5.data.insert] below.
         */
        add:x:/../*/p5.data.insert
          src:x:/@p5.data.select/*
        add:x:/../*/p5.data.insert/*
          src:x:/@sys42.cms.get-specialized-editor-data/*
        p5.data.insert

        /*
         * Then we need to refresh editor, and other widgets, but first we set edited page to the page we just saved.
         */
        sys42.cms.set-currently-edited-page:x:/@p5.web.widgets.property.get/*/*?value


        /*
         * Repopulating select page dropdown, and starts editor.
         * In addition, we need to re-create our navbar, since our navigation hierarchy has changed.
         */
        sys42.cms.populate-select-page
        sys42.cms.edit-page
        sys42.cms.create-navbar-menu
