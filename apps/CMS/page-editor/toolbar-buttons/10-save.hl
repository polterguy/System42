
/*
 * Save toolbar button for page editor.
 */
button:sys42-cms-page-editor-save-btn
  innerValue:@"<span class=""glyphicon glyphicon-floppy-save""></span> Save"
  class:btn btn-default diffuse btn-attach-top
  onclick
    sys42.save-edited-page
  events


    /*
     * Saves currently edited page
     */
    sys42.save-edited-page

      /*
       * Retrieving currently edited page.
       */
      sys42.cms.get-edited-page-id
      p5.data.select:x:@"/*/*/p5.page/""={0}"""
        :x:/@sys42.cms.get-edited-page-id?value

      /*
       * Retrieving specialized editor's data for page.
       */
      sys42.cms.get-specialized-editor-data

      /*
       * Then removing all items from [p5.data.select] that also exists in [sys42.cms.get-specialized-editor-data].
       */
      for-each:x:/@p5.data.select/*/*
        if:x:@"/@sys42.cms.get-specialized-editor-data/*/""{0}"""
          :x:/@_dp/#?name
          set:x:/@_dp/#

      /*
       * Then we can append all items from [p5.data.select] and [sys42.cms.get-specialized-editor-data] into [p5.data.update] below.
       */
      add:x:/../*/p5.data.update/*/*
        src:x:/@p5.data.select/*/*
      add:x:/../*/p5.data.update/*/*
        src:x:/@sys42.cms.get-specialized-editor-data/*

      /*
       * Updating page in p5.data database.
       */
      p5.data.update:x:@"/*/*/p5.page/""={0}"""
        :x:/@sys42.cms.get-edited-page-id?value
        src
          p5.page

      /*
       * Providing feedback to user.
       */
      sys42.windows.info-tip:Page '{0}' was successfully saved.
        :x:/@p5.data.select/*/*/name?value
