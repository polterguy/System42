
/*
 * Editor for [controls] types of pages
 */


/*
 * Loading page from database
 */
p5.data.select:x:/*/*/p5.page/"={0}"
  :x:/../*/_page-id?value


/*
 * Setting the content of editor_surface to the [lambda] from database
 */
set:x:/../*/create-widget/*/widgets/*/=editor_surface/*/value?value
  src:x:/../*/p5.data.select/*/*/lambda?value


/*
 * Creating our widget to display our editor surface
 */
create-widget
  parent:cms-specialized-editor-surface
  class:col-xs-12
  widgets
    sys42.widgets.codemirror:editor_surface
      height:555px
      value
      keys
        Alt-S:@"p5.$('sys42-cms-page-editor-save-btn').raise('onclick');"
        Alt-V:@"p5.$('sys42-cms-page-editor-view-page-btn').raise('onclick');"


      /*
       * Lambda events for Editor Surface
       */
      events


        /*
         * "Sink" event required by main editor to retrieve data
         * from specialized editor during saving process
         */
        sys42.cms.get-specialized-editor-data
          insert-before:x:/../0
            src
              type:lambda
          p5.web.widgets.property.get:editor_surface
            innerValue
          insert-after:x:/../0
            src:lambda
          set:x:/../1?value
            src:x:/../*/p5.web.widgets.property.get/*/*?value
