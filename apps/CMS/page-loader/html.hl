
/*
 * Responsible for loading up page of type [html].
 */

/*
 * Verifying user has access to [p5.page] object first.
 */
if
  sys42.cms.verify-access:x:/../*/_args/*/role?value

  /*
   * Checking if template is empty, which is not legal with an [html] type of page.
   */
  if:x:/../*/_args/*/template?value
    =:_empty
    throw:You must choose a template for your HTML pages!

  /*
   * User has access, now loading template and exchanging {content} 
   * with HTML from database.
   */
  p5.io.file.load:x:/../*/_args/*/template?value

  /*
   * Exchanging literal:{content}'s value with HTML from database.
   */
  set:x:/-/*/*/p5.web.widgets.create-container/*/widgets/**/={content}?value
    src:x:/../*/_args/*/html?value

  /*
   * Evaluating template which now has its {content} replaced with HTML from database.
   */
  eval:x:/./*/p5.io.file.load/*

else

  /*
   * Loading page that shows user does not have access to object.
   */
  sys42.utilities.execute-lambda-file:@CMS/page-loader/special-page/no-access.hl
    _template:@CMS/page-templates/default.hl


/*
 * Then making sure we modify the "header" and title of page
 */
if
  fetch:x:/0/0?value
    p5.web.widgets.exists:header
  p5.web.widgets.property.set:header
    innerValue:x:/../*/_args/*/name?value
p5.web.page.set-title:x:/../*/_args/*/name?value

