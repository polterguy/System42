
/*
 * Creating our set server salt main widget.
 */
p5.web.widgets.create-container:set-server-salt
  parent:guide
  class:flash-from-left col-xs-12 col-md-8 col-md-push-2
  widgets


    /*
     * First paragraph of informational text
     */
    literal
      element:h2
      innerValue:@"Welcome to System42"
    literal
      element:p
      innerValue:First you have to create a random server salt


    /*
     * Root password container wrapper
     */
    container
      class:form-group
      widgets

        /*
         * Textarea where user can provide a server salt.
         */
        literal:server-salt
          element:textarea
          class:form-control
          rows:10
          placeholder:Random server salt, e.g. 'sdiug45%/{huDSHJU3q796(!d'
          oninit
            p5.web.send-javascript:@"$('#server-salt').focus().select();"

        /*
         * Button which user can click to submit server salt.
         */
        literal:update-salt
          element:button
          class:col-xs-4 col-xs-push-8 col-sm-2 col-sm-push-10 btn btn-default btn-attach-top
          innerValue:Save
          onclick

            /*
             * Retrieving salt from above textarea, and storing it.
             */
            p5.web.widgets.property.get:server-salt
              value
              
            /*
             * Sanity check!
             */
            if:x:/@p5.web.widgets.property.get/*/*?value
              =:
              sys42.windows.info-tip:Sorry, you're going to have to provide a server salt!
              return

            p5.auth._set-server-salt:x:/@p5.web.widgets.property.get/*/*?value

            /*
             * Deleting main server-salt widget, and moving on to the setting of the root password.
             */
            p5.web.widgets.delete:set-server-salt
            sys42.utilities.execute-lambda-file:/system42/apps/CMS/installation/set-root-password.hl

