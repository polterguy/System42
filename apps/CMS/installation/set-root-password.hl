
/*
 * Creating our set root password main widget.
 */
p5.web.widgets.create-container:set-root-password
  parent:guide
  class:flash-from-left col-xs-12 col-md-4 col-md-push-4 col-sm-6 col-sm-push-3 col-lg-4 col-lg-push-3
  widgets


    /*
     * First paragraph of informational text
     */
    literal
      element:h2
      innerValue:@"Root password"
    literal
      element:p
      innerValue:Then create a root password


    /*
     * Root password container wrapper
     */
    container
      class:form
      widgets
        container
          class:form-group
          widgets
            container
              class:input-group
              widgets
                literal
                  class:input-group-addon
                  innerValue:Password
                void:root-password
                  element:input
                  type:password
                  class:form-control
                  placeholder:Root password ...
                  oninit
                    p5.web.send-javascript:@"$('#root-password').focus().select();"
                  onkeypress:"return p5.sys42_installation_onkeypress(event);"
            container
              class:input-group prepend-top
              widgets
                literal
                  class:input-group-addon
                  innerValue:Repeat
                void:root-password-repeat
                  element:input
                  type:password
                  class:form-control
                  placeholder:Repeat ...
                  onkeypress:"return p5.sys42_installation_onkeypress(event);"
            literal:update-root
              element:button
              class:btn btn-primary prepend-top col-sm-4 col-sm-push-8 col-xs-6 col-xs-push-6
              innerValue:Save
              onclick

                /*
                 * Retrieving password supplied by user, and sanity checking it.
                 */
                _widgets
                  root-password
                  root-password-repeat
                p5.web.widgets.property.get:x:/-/*?name
                  value
                if:x:/@p5.web.widgets.property.get/*/root-password/*?value
                  !=:x:/@p5.web.widgets.property.get/*/root-password-repeat/*?value

                  /*
                   * Passwords were not a match.
                   */
                  sys42.windows.info-tip:Sorry, passwords don't match
                    class:info-window-error
                  p5.web.send-javascript:@"$('#root-password').focus().select();"

                else

                  /*
                   * Passwords were a match, setting the root password first.
                   */
                  eval-x:x:/+/*
                  p5.auth._set-root-password
                    password:x:/@p5.web.widgets.property.get/*/root-password/*?value

                  /*
                   * Then logging in root user.
                   */
                  set:x:/+/*/password?value
                    src:x:/../*/p5.web.widgets.property.get/*/root-password/*?value
                  p5.auth.login
                    username:root
                    password

                  /*
                   * Finally deleting main set root password widget, and moving on to system finished message.
                   */
                  p5.web.widgets.delete:set-root-password
                  sys42.utilities.execute-lambda-file:@CMS/installation/system-finished.hl
                  sys42.cms.create-navbar-menu


/*
 * Making sure we trap carriage return in set password textboxes.
 */
p5.web.include-javascript:@"
p5.sys42_installation_onkeypress = function(e) {
  if(e.keyCode == 13) {
    p5.$('update-root').raise('onclick');
    return false;
  }
}"

