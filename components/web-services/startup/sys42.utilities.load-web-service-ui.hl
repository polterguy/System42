
/*
 * Creates the Active Event that allows a user to play around with a web service endpoint.
 *
 * Creates a CodeMirror instance,optionally passing in any [whitelist] arguments, such that user can play around with Hyperlambda,
 * to test the web service.
 */
create-event:sys42.utilities.load-web-service-ui

  /*
   * Making sure we include Bootstrap.
   */
  sys42.bootstrap.include-css
  sys42.bootstrap.include-javascript
  p5.web.include-css-file:@CMS/media/css/main.min.css
  p5.web.include-css-file:@CMS/media/css/backgrounds/cogs.min.css

  /*
   * Creating main "content" wrapper, if there does not already exist one.
   */
  if
    fetch:x:/0/0?value
      p5.web.widgets.exists:content
    not

    /*
     * Making sure we create a default sane layout, and include "content" container on page, to have common windows and such working.
     */
    p5.web.widgets.create-container:content
      class:row
      parent:cnt
      widgets
        h1
          class:col-xs-12
          innerValue:Lambda Web Service
    p5.web.widgets.property.set:cnt
      class:container

  /*
   * Creating CodeMirror instance.
   */
  if:x:/../*/whitelist
    add:x:/../*/sys42.widgets.codemirror-executor
      src:x:/../*/whitelist
  sys42.widgets.codemirror-executor
  set:x:/@sys42.widgets.codemirror-executor/0?name
    src:p5.web.widgets.create-container
  set:x:/@sys42.widgets.codemirror-executor/0?value
    src:codemirror-executor
  add:x:/@sys42.widgets.codemirror-executor/0
    src
      parent:content
  eval:x:/@sys42.widgets.codemirror-executor
