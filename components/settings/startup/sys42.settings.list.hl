
/*
 * Creates the Active Event that returns all application specific settings.
 *
 * Arguments;
 *
 * [app]          - Name of application to which setting belongs.
 * [username]     - Username to which settings belongs to.
 *
 * Optionally, pass in a filter as an [_arg] to filter results.
 */
create-event:sys42.settings.list

  /*
   * Sanity check
   */
  if:x:/../*/app?value
    not
    or:x:/../*/app?value
      =:
    throw:No application specified!

  /*
   * Retrieving currently logged in user, checking if user is "root", and if a specific user was supplied.
   */
  whoami
  if:x:/@whoami/*/role?value
    =:root
    and:x:/../*/username?value

    /*
     * Changing user to retrieve setting for in [select-data] below.
     */
    set:x:/../*/select-data/0?value
      src:x:/../*/username?value

  else-if:x:/../*/username?value

    /*
     * Non root account tried to change setting for another user!
     */
    throw:Only root accounts can retrieve settings for other users than the currently logged in identity user

  /*
   * Checking if caller provided a filter, and if so, making sure we use it during [select-data].
   */
  if:x:/../*/_arg?value
    set:x:/../*/select-data/2?value
      src:/~{0}
        :x:/../*/_arg?value

  select-data:x:@"/*/*/sys42.app-settings/*/username/={0}/./*/app-name/""={1}""/./*{2}"
    :x:/@whoami/*/username?value
    :x:/../*/app?value
    :

  /*
   * Returning setting value(s).
   */
  return:x:/@select-data/*(!/username!/app-name)


