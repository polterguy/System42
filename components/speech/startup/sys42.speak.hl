
/*
 * Creates the event that allows caller to create speech synthesis on the client side.
 * Pass in [_arg] as whatever you want to have spoken, and [voice] being the locale language you wish to have it spoken in.
 *
 * Example of some [voice]s you could use are; "en-US" for US English, and "nb-NO" for Norwegian Bokm√•l. You can also pass in a named
 * [voice], such as "Fred", "Samantha" or "Nora".
 *
 * Optionally, you can pass in [voice] as "Fred,en-US" for instance, which will ensure that a voice explicitly named "Fred"
 * will be attempted to be used, if it exists, defaulting to the first "en-US" voice that exists, if no "Fred" voice exists.
 * The latter allows you to prioritize a specific "voice", resorting to the first language voice of your choice, if the named 
 * voice does not exist.
 */
create-event:sys42.speak

  /*
   * Making sure we include our JavaScript file.
   */
  p5.web.include-javascript-file:@SYS42-COMPONENTS/speech/media/speech.js


  /*
   * Passing in JavaScript to client that creates the speech, making sure we [join] arguments, if multiple [_arg]s are 
   * supplied, in addition to making sure we escape any double quotes (").
   */
  join:x:/../*/_arg?value
    sep:" "
  replace:x:/@join?value
    src:@""""
    dest:@"\"""
  replace:x:/-?value
    src:"\r\n"
    dest:"\\r\\n"

  /*
   * Making sure we invoke client-side function that wraps our speech.
   */
  p5.web.send-javascript:@"p5.speech.speak(""{0}"", ""{1}"");"
    :x:/@replace?value
    :x:/../*/voice?value