
/*
 * Creates the Active Event that imports a CSV file.
 * Imports the given [_arg] CSV file, and puts into p5.data database.
 */
create-event:sys42.csv.import

  if:x:/../*/_arg?count
    !=:int:1
    throw:We can only import one CSV file at the time!

  /*
   * Loading CSV file.
   */
  p5.io.file.load:x:/../*/_arg?value

  /*
   * Needed to make sure filename becomes type of object to [p5.data.insert].
   */
  p5.string.split:x:/../*/p5.io.file.load/*?name
    =:/

  /*
   * Looping through each row in file, except the first row, 
   * which are column headers.
   */
  for-each:x:/@p5.io.file.load/*/*/[1,]

    /*
     * Adding database type and column headers to [p5.data.insert].
     */
    add:x:/../*/p5.data.insert
      src:x:/@p5.string.split/0/-?name
    add:x:/../*/p5.data.insert/0/-
      src:x:/@p5.io.file.load/0/0/*?name

    /*
     * Looping through each column in row.
     */
    _no:int:0
    while:x:/@_no?value
      <:x:/../*/p5.data.insert/0/-/*?count

      /*
       * Settings currently iterated column value for currently iterated row.
       */
      set:x:/../*/p5.data.insert/0/-/{0}?value
        :x:/@_no?value
        src:x:/@_dp/#/{0}?name
          :x:/@_no?value

      /*
       * Incrementing [while] iteration integer.
       */
      set:x:/@_no?value
        +:x:/@_no?value
          _:int:1

  /*
   * Making sure we insert all our data in "one go", to reduce the number of locks on database, and increase the speed of the operation.
   */
  p5.data.insert
