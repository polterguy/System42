
/*
 * Supporting Active Event, to create URL from given [_arg] value, with the given [_crawl-get-name] as the HTTP argument name.
 */
create-event:sys42.widgets.tab._create-url

  /*
   * First we get current location, without GET parameters, before we add the question mark to make sure we 
   * "open" our GET params collection.
   */
  p5.web.get-location-url
  set:x:/@p5.web.get-location-url?value
    src:{0}?
      :x:/@p5.web.get-location-url?value

  /*
   * Then we must fetch all GET parameters, and remove any previously created "sys42_widgets_tab_unroll" params,
   * before we add all other GET parameters, to make sure we keep everything EXCEPT "sys42_widgets_tab_unroll".
   */
  p5.web.query.list
  set:x:/@p5.web.query.list/*/{0}
    :x:/../*/crawl-get-name?value
  if:x:/@p5.web.query.list/*
    p5.web.query.get:x:/@p5.web.query.list/*?name
    for-each:x:/@p5.web.query.get/*
      p5.html.url-encode:x:/@_dp/#?value
      set:x:/@p5.web.get-location-url?value
        src:{0}{1}={2}&
          :x:/@p5.web.get-location-url?value
          :x:/@_dp/#?name
          :x:/@p5.html.url-encode?value

  /*
   * Adding key for our "unroll" GET parameter.
   */
  set:x:/../*/p5.web.get-location-url?value
    src:{0}{1}=
      :x:/../*/p5.web.get-location-url?value
      :x:/../*/crawl-get-name?value

  /*
   * Then we can URL encode the parameter, and return URL to caller.
   */
  p5.html.url-encode:x:/../*/_arg?value

  return:{0}{1}
    :x:/../*/p5.web.get-location-url?value
    :x:/@p5.html.url-encode?value

