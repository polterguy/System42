
/*
 * Starts editing a single cell in a DataGrid.
 *
 * Invoked when a cell's hyperlink (button) is clicked.
 * Expects [_hyperlink] being the ID of the widget (hyperlink) that was actually clicked, in addition to [_arg] being
 * the ID of the datagrid.
 */
create-event:sys42.widgets.datagrid._cell-clicked

  /*
   * Fetching column name and innerValue.
   */
  p5.web.widgets.property.get:x:/../*/_hyperlink?value
    _column
    innerValue

  /*
   * Fetching row ID by retrieving parent's parent widget's [_row] value.
   */
  p5.web.widgets.find-ancestor:x:/../*/_hyperlink?value
    _row
  p5.web.widgets.property.get:x:/@p5.web.widgets.find-ancestor/*/*?value
    _row

  /*
   * Forward evaluating all arguments, before we invoke [_edit-item] event, responsible for creating
   * the edit widget UI.
   */
  eval-x:x:/+/*
  sys42.widgets.datagrid._edit-cell:x:/../*/_arg?value
    hyperlink:x:/../*/_hyperlink?value
    column:x:/../*/p5.web.widgets.property.get/[0,1]/*/*/_column?value
    value:x:/../*/p5.web.widgets.property.get/[0,1]/*/*/innerValue?value
    row:x:/@p5.web.widgets.property.get/*/*?value
