
/*
 * Pages the specified [_arg] datagrid to its next page.
 */
create-event:sys42.widgets.datagrid.go-next

  /*
   * Retrieving current page and number of items per page.
   */
  p5.web.widgets.property.get:x:/../*/_arg?value
    _current-page
    _page-size
    _query

  /*
   * Incrementing page by 1, and storing results in above [p5.web.widgets.property.get]'s result.
   */
  set:x:/@p5.web.widgets.property.get/*/*/_current-page?value
    +:x:/@p5.web.widgets.property.get/*/*/_current-page?value.int
      _:1

  /*
   * Updating current page to property of datagrid root widget.
   */
  p5.web.widgets.property.set:x:/../*/_arg?value
    _current-page:x:/@p5.web.widgets.property.get/*/*/_current-page?value

  /*
   * Databinding grid, and if it was unsuccessful, we decrement current page back to what it was previously.
   */
  if
    sys42.widgets.datagrid.databind:x:/../*/_arg?value
    not
    set:x:/@p5.web.widgets.property.get/*/*/_current-page?value
      -:x:/@p5.web.widgets.property.get/*/*/_current-page?value.int
        _:1
    p5.web.widgets.property.set:x:/../*/_arg?value
      _current-page:x:/@p5.web.widgets.property.get/*/*/_current-page?value
