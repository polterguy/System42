
/*
 * Filters specified [_arg] datagrid according to specified [query].
 */
create-event:sys42.widgets.datagrid.filter

  /*
   * Making sure we update pager, such that the first page is displayed.
   */
  p5.web.widgets.property.set:x:/../*/_arg?value
    _current-page:0

  /*
   * Storing query in datagrid root widget, if we are given a query.
   * Otherwise, we entirely delete [query] property on widget.
   */
  if:x:/../*/query?value
    and:x:/../*/query?value
      !=:

    /*
     * We have a query, making sure we store query in datagrid main widget.
     */
    p5.web.widgets.property.set:x:/../*/_arg?value
      _query:x:/../*/query?value

  else

    /*
     * We no not have a query, deleting [_query] property from datagrid, and making sure our [_get-items] 
     * invocation does not specify any [query] parameter.
     */
    p5.web.widgets.property.delete:x:/../*/_arg?value
      _query

  /*
   * Databind datagrid.
   */
  sys42.widgets.datagrid.databind:x:/../*/_arg?value
