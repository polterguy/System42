
/*
 * Creates an Ajax uploader widget, which serves as a "dropzone" for dragging and dropping files from your desktop, and unto
 * your browser's surface. Provide at the very least an [.onupload] lambda callback handler, which will be invoked whenever the 
 * user drops one or more files unto its surface.
 *
 * Arguments;
 *
 * [css-file]             - A CSS file to automatically include into the page as the widget is instantiated.
 *                          Its default value is "uploader.min.css", which can be found in the "media" folder of the widget's
 *                          folder structure. If you set it to "" (empty string), then no CSS file will be included.
 * [class]                - The CSS class for your widget. Default value is "uploader-widget", which corresponds with the class
 *                          inside of "uploader.min.css".
 * [dragover-class]       - The CSS class to use whenever some file(s) is dragged over your widget. Useful to give the user hints that
 *                          it accepts dropping of files. The default value is "uploader-widget-dragover".
 * [drop-class]           - The CSS class to use whenever some file(s) are dropped unto the widget's surface. Useful to give
 *                          visual clues to the user that an uploading of one or more files is in progress. The default value
 *                          is "uploader-widget-drop".
 * [error-class]          - CSS class used for the widget when there is some sort of error, due to for instance trying to upload a file
 *                          with the wrong extension, or trying to upload multiple files, when the uploader only accepts a single file.
 * [allow-multiple]       - If false, then only one file is allowed being uploaded at the same time. The default value is true.
 * [filter]               - Pipe separated (|) list of  valid file extensions that the uploader accepts. Default value is empty, "", 
 *                          which means "everything is accepted".
 * [clickable]            - If false, will not let the user click the widget to open up browse for file window.
 * [preview]              - If false, will not create previews of files during upload.
 * [.onupload]            - Lambda callback which will be invoked whenever some file have been uploaded to the server. Will be given
 *                          a [_filename] and a [_content] argument.
 *                          In addition, for cases when the user is uploading multiple files at once, it will also be given the
 *                          arguments [_count] and [_current], which are the total number of files, and the currently uploaded file.
 * [events]               - Additional set of custom events to be associated with widget.
 */
create-event:sys42.widgets.uploader

  /*
   * Applying options for widget, with default values.
   */
  _defaults
    css-file:/system42/components/common-widgets/uploader/media/uploader.css
    class:uploader-widget
    dragover-class:uploader-widget-dragover
    drop-class:uploader-widget-drop
    error-class:uploader-widget-error
    allow-multiple:bool:true
    filter
    clickable:true
    preview:true
    .onupload
  _options
  add:x:/@_options
    src:x:(/@_defaults(/--!/_arg)|/@_defaults/*)/$


  /*
   * Adding [.onupload] lambda callback argument given into [_onupload] Ajax event below
   */
  add:x:/../*/return/*/container/*/.onupload/*/.lambda
    src:x:/../*/_options/*/.onupload/*


  /*
   * Making sure we remove the role=button parts, if widget is not clickable.
   */
  if:x:/../*/_options/*/clickable?value.bool
    not
    set:x:/../*/return/*/container/*/role


  /*
   * Making sure we remove the "multiple" attribute, if widget does not allow for multiple files.
   */
  if:x:/../*/_options/*/allow-multiple?value.bool
    not
    set:x:/../*/return/*/container/*/widgets/*/void/*/multiple


  /*
   * Adding all custom lambda events specified, if any.
   */
  add:x:/../*/return/*/container
    src:x:/../*/events


  /*
   * Forward evaluating some arguments to widget.
   */
  eval-x:x:/../*/return/**(/container/*/class|/oninit/*(/_class|/_dragover-class|/_drop-class|/_error-class|/_filter|/_allow-multiple|/_css-file|/_clickable|/_preview)|/return/*/container/*/class|/.onupload/*(/_filter|/_allow-multiple))


  /*
   * Returning the uploader widget to caller.
   */
  return
    container
      element:div
      class:x:/../*/_options/*/class?value
      role:button
      oninit

        /*
         * Including CSS file, if a CSS file was specified.
         * This one is forward evaluated above.
         */
        _css-file:x:/../*/_options/*/css-file?value
        if:x:/@_css-file?value
          p5.web.include-css-file:x:/@_css-file?value

        /*
         * Including JavaScript file that contains uploader widget's client-side JavaScript.
         */
        p5.web.include-javascript-file:/system42/components/common-widgets/uploader/media/uploader.js

        /*
         * These are forward evaluated during construction of widget.
         */
        _class:x:/../*/_options/*/class?value
        _dragover-class:x:/../*/_options/*/dragover-class?value
        _drop-class:x:/../*/_options/*/drop-class?value
        _error-class:x:/../*/_options/*/error-class?value
        _filter:x:/../*/_options/*/filter?value
        _allow-multiple:x:/../*/_options/*/allow-multiple?value
        p5.string.to-lower:x:/-?value.string
        _clickable:x:/../*/_options/*/clickable?value
        p5.string.to-lower:x:/-?value.string
        _preview:x:/../*/_options/*/preview?value
        p5.string.to-lower:x:/-?value.string

        /*
         * Making sure we pass in file element to uploader instantiation.
         */
        p5.web.widgets.find:x:/../*/_event?value
          element:input
          type:file

        /*
         * Making sure we include our uploader JavaScript file, and our initialize uploader JavaScript.
         * Notice, we have to wait until widget is actually attached before we do this, since otherwise we won't know its ID,
         * unless an explicit ID was provided, which is not necessary the case.
         */
        p5.web.send-javascript:@"p5['{0}'] = new p5.uploader ('{0}', '{1}', '{2}', '{3}', '{4}', '{5}', {6}, '{7}', {8}, {9});"
          :x:/../*/_event?value
          :x:/../*/_class?value
          :x:/../*/_dragover-class?value
          :x:/../*/_drop-class?value
          :x:/../*/_error-class?value
          :x:/../*/_filter?value
          :x:/../*/p5.string.to-lower/[0,1]?value
          :x:/@p5.web.widgets.find/*/*?value
          :x:/../*/p5.string.to-lower/[1,2]?value
          :x:/../*/p5.string.to-lower/[2,3]?value

      /*
       * Event raised when a file is uploaded.
       */
      .onupload

        /*
         * Retrieving content, filename, count and current index that was uploaded.
         */
        p5.web.params.get:sys42.widgets.uploader.count
        p5.web.params.get:sys42.widgets.uploader.current
        p5.web.params.get:sys42.widgets.uploader.filename
        p5.web.params.get:sys42.widgets.uploader.content

        /*
         * Server side validation of filename's extension, to avoid client-side hacking of JS, to allow uploading of non-supported files!
         */
        _filter:x:/../*/_options/*/filter?value
        if:x:/@_filter?value
          split:x:/@_filter?value
            =:|
          split:x:/../*/p5.web.params.get/*/sys42.widgets.uploader.filename?value
            =:.
          if:x:/@split/@split/*/{0}
            :x:/@split/0/-?name
            not

            /*
             * Illegal file extension!
             * Providing some visual clues, and returning early.
             */
            sys42.windows.info-tip:Unsupported file extension!
              class:info-window info-window-error
            return

        /*
         * Checking if multiple files were provided, and if this is legal according to widget's properties.
         */
        _allow-multiple:x:/../*/_options/*/allow-multiple?value.bool
        if:x:/../*/p5.web.header.get/*/sys42.widgets.uploader.count?value.int
          >:int:1
          and:x:/@_allow-multiple?value.bool
            =:bool:false
          sys42.windows.info-tip:Please only upload one file at the time!
            class:info-window info-window-error
          return

        /*
         * Decoding from base64 to byte[].
         */
        p5.string.decode-base64:x:/../*/p5.web.params.get/*/sys42.widgets.uploader.content?value

        /*
         * Making sure we "isolate" lambda callback supplied during instantiation of widget.
         */
        .lambda
        eval-x:x:/+/*
        eval:x:/@.lambda
          _count:x:/../*/p5.web.params.get/[0,1]/*?value
          _current:x:/../*/p5.web.params.get/[1,2]/*?value
          _filename:x:/../*/p5.web.params.get/[2,3]/*?value
          _content:x:/../*/p5.string.decode-base64?value
          _event:x:/../*/_event?value

      widgets
        void
          element:input
          type:file
          multiple
          style:"opacity:0 !important;position:absolute !important;width:0;height:0;z-index:-1;top:-5000px;left:0;"
