
/*
 * Creates the Active Event that shows the user a modal window, asking user for some input.
 *
 * Arguments;
 * 
 * [header]          - Header of modal dialogue
 * [body]            - Explanatory text of dialogue
 * [class]           - Override the default CSS class
 * [inner-class]     - Override the default inner CSS class. Set this to "modal-dialog modal-lg" to create a wider dialogue.
 * [buttons]         - Makes it possible to exchange the default "OK" button with your own buttons
 *                     If you supply a [buttons] parameter, then [_ok] has no effect, and you must
 *                     provide your own logic in the onclick handler of these buttons instead.
 *                     To evaluate the [.onok] lambda, you can invoke [sys42.windows.modal.ok],
 *                     To close dialogue, you can invoke [sys42.windows.modal.destroy]. If you wish to
 *                     make sure your [.oncancel] lambda is evaluated, you should use [sys42.windows.modal.cancel]
 *                     instead. [sys42.windows.modal.destroy] will not evaluate your [.oncancel] lambda callback.
 * [data]            - Will automatically create a widget collection, according to the type of data supplied.
 *                     The default widget type to use, is a normal textbox. However, if you supply a child node with the name of
 *                     [_options], it will create a select HTML element instead, with the option elements supplied as children of [_options].
 * [.onok]           - Code to be evaluated if user clicks OK button
 * [.oncancel]       - Lambda callback to be evaluated if user closes dialogue without pressing "OK".
 *
 * If user closes modal dialogue, without clicking OK, then the given [.onok] is not evaluated.
 * There can be only one instance of this window at the same time in your page.
 */
create-event:sys42.windows.wizard

  /*
   * Simply using the [sys42.windows.modal] to create our wizard window.
   * First applying all arguments into [.lambda], which contains our [sys42.windows.modal] invocation.
   */
  add:x:/../*/.lambda/*
    src:x:/./--

  /*
   * Sanity checking arguments.
   */
  if:x:/../*/header?value
    not
    add:x:/../*/.lambda/*
      src
        header:Please supply input!
  if:x:/../*/body
    not
    add:x:/../*/.lambda/*
      src
        body:<p>Please supply me with some input</p>
  if:x:/../*/widgets
    throw:You cannot supply a [widgets] collection to [sys42.windows.wizard]. Use [sys42.windows.modal] instead.

  /*
   * Looping through each [data] items, creating an edit widget for each item.
   *
   * Notice, by default, this creates a textbox "input" HTML widget for each child node of [data], unless the node contains an [_options] child,
   * at which case it will create a "select" HTML widget instead, with the currently active value, being the value of the currently iterated
   * [data] child.
   *
   * If there's a child called [multiline] with boolean "true", it will create a textarea instead of textbox input.
   */
  for-each:x:/../*/data/*

    /*
     * Checking what type of edit field this is.
     */
    if:x:/@_dp/#/*/_options

      /*
       * Select dropdown choices.
       * Creating our "option" widgets through a lambda callback, which will return the [value] and [innerValue] for the currently
       * iterated "option" element. Making sure we set the [_selected] value for our "select" widget, inside of our lambda, such that
       * it can check if currently iterated "option" element is the initially selected option.
       */
      set:x:/+/*/_selected?value
        src:x:/@_dp/#?value

      /*
       * Used as a lambda callback for our [apply] invocation below.
       */
      _eval-option
        _selected

        /*
         * Returning [value] and [innerValue] to caller.
         */
        eval-x:x:/+/*/*
        add:x:/../*/return
          src
            value:x:/@_dn/#?value
            innerValue:x:/@_dn/#?name

        /*
         * Checking if currently iterated "option" element is selected, and if so, adding [selected] attribute to return lambda.
         */
        if:x:/../*/_selected?value
          =:x:/../*/_dn/#?value
          add:x:/../*/return
            src:selected

        /*
         * Returning [value], [innerValue] and possibly also [selected] to caller, which is our [apply] below.
         */
        return

      /*
       * Using [apply] to create our "option" HTML widgets, and append into [widgets] of [select] below.
       */
      apply:x:/+2/**/select/*/widgets
        src:x:/@_dp/#/*/_options/*
        template
          option
            {@eval}:x:/..if/*/_eval-option

      /*
       * Forward evaluating other arguments.
       */
      eval-x:x:/+/**(/label|/select)/*
      add:x:/../*/.lambda/*/sys42.windows.modal/*/widgets
        src
          container
            class:form-group
            widgets
              container
                class:input-group
                widgets
                  label
                    class:input-group-addon
                    innerValue:x:/@_dp/#?name
                  select
                    class:form-control
                    value:x:/@_dp/#?value
                    _data-field-name:x:/@_dp/#?name
                    widgets

    else

      /*
       * Defaulting to "textbox", but first checking if it's a multiline string
       */
      if
        fetch:x:/0/0?value
          index-of:x:/@_dp/#?value
            src:"\r\n"
        or:x:/@_dp/#/*/multiline?value.bool
        set:x:/..else/*/add/**/input/*/type
        set:x:/..else/*/add/**/input/*/value?name
          src:innerValue
        add:x:/..else/*/add/**/input
          src
            rows:3
        set:x:/..else/*/add/**/input?name
          src:textarea
      eval-x:x:/+/**(/label|/input|/textarea)/*
      add:x:/../*/.lambda/*/sys42.windows.modal/*/widgets
        src
          container
            class:form-group
            widgets
              container
                class:input-group
                widgets
                  label
                    class:input-group-addon
                    innerValue:x:/@_dp/#?name
                  input
                    type:text
                    class:form-control
                    value:x:/@_dp/#?value
                    _data-field-name:x:/@_dp/#?name

  /*
   * Making sure we set initial focus to the first widget in our [widgets] collection.
   */
  if:x:/../*/.lambda/*/sys42.windows.modal/*/widgets/*?count
    >:int:0

    /*
     * There exists a [widgets] collection in invocation to [sys42.windows.modal].
     *
     * Making sure we give initial focus to first widget automatically.
     */
    add:x:/../*/.lambda/*/sys42.windows.modal/*/widgets/0/*/widgets/0/*/widgets/1
      src
        oninit
          sys42.windows.modal.initial-focus:x:/../*/_event?value

  /*
   * Now we can evaluate our modal window, with a dynamically built [widgets] collection, created above.
   */
  .lambda
    sys42.windows.modal
      widgets
  eval:x:/@.lambda
