
/*
 * Creates the Active Event that shows the user a modal confirmatiom window.
 *
 * Asks the user to confirm some action, before action is evaluated. Pass in action as [.onok]. Optionally, pass in
 * an [.oncancel] lambda.
 *
 * Arguments;
 * 
 * [header]          - Header of modal dialogue
 * [body]            - Explanatory text of dialogue
 * [class]           - Override the default CSS class
 * [inner-class]     - Override the default inner CSS class. Set this to "modal-dialog modal-lg" to create a wider dialogue.
 * [buttons]         - Makes it possible to exchange the default "OK" button with your own buttons
 *                     If you supply a [buttons] parameter, then [_ok] has no effect, and you must
 *                     provide your own logic in the onclick handler of these buttons instead.
 *                     To evaluate the [.onok] lambda, you can invoke [sys42.windows.modal.ok],
 *                     To close dialogue, you can invoke [sys42.windows.modal.destroy]. If you wish to
 *                     make sure your [.oncancel] lambda is evaluated, you should use [sys42.windows.modal.cancel]
 *                     instead. [sys42.windows.modal.destroy] will not evaluate your [.oncancel] lambda callback.
 * [.onok]           - Code to be evaluated if user clicks OK button
 * [.oncancel]       - Lambda callback to be evaluated if user closes dialogue without pressing "OK".
 *
 * If user closes modal dialogue, without clicking OK, then the given [.onok] is not evaluated.
 * There can be only one instance of this dialogue at the same time in your page.
 */
create-event:sys42.windows.confirm

  /*
   * Simply using the [sys42.windows.modal] to create our confirm window.
   * First applying all arguments into [.lambda], which contains our [sys42.windows.modal] invocation.
   */
  add:x:/../*/.lambda/*
    src:x:/./--

  /*
   * Sanity checking arguments.
   */
  if:x:/../*/header?value
    not
    add:x:/../*/.lambda/*
      src
        header:Please confirm action!
  if:x:/../*/body?value
    not
    add:x:/../*/.lambda/*
      src
        body:<p>Please confirm you really want to do this</p>
  if:x:/../*/_widgets
    throw:You cannot supply a [_widgets] collection to [sys42.windows.confirm]. Use [sys42.windows.modal] instead.

  /*
   * Checking if caller supplied a [buttons] collection, and if not, we create a default "OK" button, which
   * has initial focus, and simply invokes [sys42.windows.modal.ok] when clicked.
   */
  if:x:/../*/buttons/*
    not
    add:x:/../*/.lambda/*
      src
        buttons
          literal
            element:button
            class:btn btn-default
            innerValue:OK
            oninit
              sys42.windows.modal.initial-focus:x:/@_event?value
            onclick
              sys42.windows.modal.ok
  .lambda
    sys42.windows.modal
  eval:x:/@.lambda
