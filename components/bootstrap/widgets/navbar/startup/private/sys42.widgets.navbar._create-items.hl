
/*
 * Supporting Active Event for the Navbar widget, to create items.
 *
 * This Active Event creates the Navbar items, for your menu, recursively.
 */
create-event:sys42.widgets.navbar._create-items

  /*
   * Checking if we have items, and if not, returning early.
   */
  if:x:/../*/items/#/*
    not
    return

  /*
   * Looping through all [items].
   */
  for-each:x:/../*/items/#/*

    /*
     * Checking if this item is nested.
     */
    if:x:/@_dp/#/*/items

      /*
       * This item has nested children.
       *
       * Forward evaluating [items] collection in recursive invocation of "self" inside of [add], to make sure
       * we recursively add children [items] of currently iterated [items] item.
       */
      eval-x:x:/+/*/*
      add:x:/..if/*/add/[1,2]/*/*/*/widgets/*/ul/*/widgets
        sys42.widgets.navbar._create-items:x:/../*/_arg?value
          items:x:/@_dp/#/*/items
          crawl:x:/../*/crawl?value
          crawl-get-name:x:/../*/crawl-get-name?value

      /*
       * Setting properties for currently iterated item, and adding item into [return] below.
       */
      eval-x:x:/..if/*/add/[1,2]/*/*/*/widgets/*/a/*/innerValue

      /*
       * Adding currently iterated [items] to [return] below, now with the correct [innerValue] and [_item-id] according
       * to caller's request.
       */
      add:x:/../*/return
        src
          li
            class:dropdown
            widgets
              a
                href:#
                class:dropdown-toggle
                data-toggle:dropdown
                role:button
                aria-haspopup:true
                aria-expanded:false
                innerValue:@"{0} <span class=""caret""></span>"
                  :x:/@_dp/#?name
              ul
                class:dropdown-menu
                widgets
    else-if:x:/@_dp/#?name
      =:_separator

      /*
       * This is a separator item.
       */
      add:x:/../*/return
        src
          li
            class:divider
            role:separator

    else

      /*
       * This item does not have any nested children.
       * Adding the supplied [.onclick] to our currently iterated item.
       */
      add:x:/..else/*/add/[1,2]/*/*/*/widgets/*/a/*/onclick
        src:x:/@_dp/#/*/.onclick/*

      /*
       * Setting properties for currently iterated item, and adding item into [return] below.
       */
      eval-x:x:/..else/*/add/[1,2]/*/*/*/widgets/*/a/*(/innerValue|/_item-id)

      /*
       * Checking if caller wanted to have an SEO friendly navbar.
       */
      if:x:/../*/crawl?value.bool
        not

        /*
         * Removing [oninit], since it's only there to retrieve the SEO URL, and caller did not want an SEO navbar.
         */
        set:x:/..else/*/add/[1,2]/*/*/*/*/*/oninit

      /*
       * Adding currently iterated [items] to [return] below, now with the correct [innerValue] and [_item-id] according
       * to caller's request.
       */
      add:x:/../*/return
        src
          li
            widgets
              a
                href:#
                role:button
                _item-id:x:/@_dp/#?value
                innerValue:x:/@_dp/#?name
                oninit
                  sys42.widgets.navbar._create-item-url:x:/../*/_event?value
                  p5.web.widgets.property.set:x:/../*/_event?value
                    href:x:/@sys42.widgets.navbar._create-item-url?value
                onclick

                  /*
                   * This might look empty, but it contains the lambda callback supplied
                   * by caller for [.onclick] of item, if any.
                   */
                  if
                    p5.web.request.is-ajax-callback
                    p5.web.send-javascript:@"if ($(window).width() < 768) {$('.navbar-toggle').click();}"
                  p5.web.widgets.find-ancestor-like:x:/../*/_event?value
                    element:ul
                    class:dropdown-menu
                  if:x:/@p5.web.widgets.find-ancestor-like/*/*?value
                    and
                      p5.web.request.is-ajax-callback
                    p5.web.send-javascript:@"$('#{0}').dropdown('toggle');"
                      :x:/@p5.web.widgets.find-ancestor-like/*/*?value

  /*
   * Returning widget collection to caller.
   */
  return
