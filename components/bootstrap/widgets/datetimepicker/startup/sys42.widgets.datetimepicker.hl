
/*
 * Creates an Ajax DateTimePicker widget, which allows the user to pick a date, and optionally a time.
 *
 * Arguments;
 * 
 * [label]              - If declared, will create a descriptive input-group-addon label, with the specified content.
 * [class]              - The CSS class to use for your DateTimePicker. Defaults to "input-group date".
 * [icon]               - A CSS class, which is used instead of the default 'glyphicon-calendar' class for the 'dropdown button'.
 * [locale]             - Which locale to use. Is basically a reference to the JS files in the 'media/js/locale/' folder, and are helpers
 *                        to format the dates according to whatever is common in whatever language you want your DateTimePicker rendered with.
 * [format]             - Declares how to format the date in the textbox.
 * [defaultDate]        - Which date to use by default.
 * [view-mode]          - Declares how the DateTimePicker should be rendered.
 * [min-date]           - Declares the minimum valid date. Expected to be a date type, or date formatted string.
 * [max-date]           - Declares the maximum valid date. Expected to be a date type, or date formatted string.
 * [show-today-button]  - If true, will show the "select today button".
 * [.onchange]          - Lambda callback invoked when the value of the DateTimePicker changes.
 *
 * In addition, the DateTimePicker contains the following Active Events.
 *
 * [sys42.widgets.datetimepicker.get-value]     - Retrieves the value of the widget. Expects the ID of the widget as its [_arg].
 * [sys42.widgets.datetimepicker.set-value]     - Sets the value of the widget. Expects the ID of the widget as its [_arg], in addition
 *                                                to a [_value] being a legal value for the widget.
 *
 * To understand the legal values for the above arguments, please read the documentation for the Bootstrap DateTimePicker here;
 * https://eonasdan.github.io/bootstrap-datetimepicker/
 */
create-event:sys42.widgets.datetimepicker

  /*
   * Applying options for widget, with default values.
   */
  _defaults
    label
    class:input-group date
    icon:glyphicon glyphicon-calendar
    locale
    format
    defaultDate
    view-mode
    min-date
    max-date
    show-today-button
    .onchange
  _options
  add:x:/@_options
    src:x:(/@_defaults(/--!/_arg)|/@_defaults/*)/$

  /*
   * Checking if [label] was passed in, and if so, making sure widget gets a descriptive input-group-addon.
   */
  if:x:/../*/_options/*/label?value
    eval-x:x:/+/*/*/*
    insert-before:x:/../*/return/*/container/*/widgets/*/input
      src
        label
          class:input-group-addon
          innerValue:x:/../*/_options/*/label?value

  /*
   * Checking if no [icon] was passed in, and if not, deleting addon entirely, making input element itself clickable.
   */
  if:x:/../*/_options/*/icon?value
    not
    set:x:/../*/return/*/container/*/widgets/*/span

  /*
   * Passing in [.onchange] lambda callback, such that it is evaluated upon change.
   */
  if:x:/../*/_options/*/.onchange/*

    /*
     * We have an [.onchange] lambda callback, making sure we initialize DateTimePicker such that it invokes
     * it during "dp.hide", by adding the callback lambda into [.onchange] of "input" element.
     */
    add:x:/../*/return/*/container/*/widgets/*/input/*/.onchange/*/.lambda
      src:x:/../*/_options/*/.onchange/*

  else

    /*
     * We do not have an [.onchange] lambda callback. Making sure datetimepicker is initialized without
     * the "dp.hide" event handled, by removing [.onchange] lambda callback of "input" element.
     */
    set:x:/../*/return/*/container/*/widgets/*/input/*/.onchange

  /*
   * Passing in arguments to [oninit].
   */
  add:x:/../*/return/**/oninit
    src:x:/../*/_options

  /*
   * Forward evaluating arguments.
   */
  eval-x:x:/../*/return/*/container/*(/class|/widgets/*/span/*/innerValue|/widgets/*/input/*/value)

  /*
   * Returning datetimepicker to caller.
   */
  return
    container
      class:x:/../*/_options/*/class?value
      widgets

        /*
         * Textbox containing value of datetimepicker.
         */
        input
          type:text
          class:form-control
          value:x:/../*/_options/*/_value?value

          oninit

            /*
             * Including Bootstrap (and jQuery)
             */
            sys42.bootstrap.include-javascript
            sys42.bootstrap.include-css

            /*
             * The DateTimePicker is dependent upon Moment.js.
             */
            p5.web.include-javascript-file:@BOOTSTRAP/widgets/datetimepicker/media/js/moment.min.js

            /*
             * Including DateTimePicker's JavaScript and CSS
             */
            p5.web.include-javascript-file:@BOOTSTRAP/widgets/datetimepicker/media/js/bootstrap-datetimepicker.min.js
            p5.web.include-css-file:@BOOTSTRAP/widgets/datetimepicker/media/css/bootstrap-datetimepicker.min.css

            /*
             * Becomes the arguments to pass into JavaScript instantiation.
             * This is dynamically modified, to become a JavaScript option object, according to the logic below.
             */
            _arguments

            /*
             * Checking if [locale] was passed in, and if so, making sure we use specified locale.
             */
            if:x:/../*/_options/*/locale?value

              /*
               * Including the necessary "locale" JS file, and setting the "locale" option for our [_argument] above.
               */
              p5.web.include-javascript-file:@"@BOOTSTRAP/widgets/datetimepicker/media/js/locale/{0}.js"
                :x:/../*/_options/*/locale?value
              set:x:/@_arguments?value
                src:@"{0},locale:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/locale?value

            /*
             * Checking if [format] was passed in, and if so, making sure we use specified format.
             */
            if:x:/../*/_options/*/format?value
              set:x:/@_arguments?value
                src:@"{0},format:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/format?value

            /*
             * Checking if [_default-date] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/_default-date?value
              set:x:/@_arguments?value
                src:@"{0},defaultDate:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/_default-date?value.string

            /*
             * Checking if [view-mode] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/view-mode?value
              set:x:/@_arguments?value
                src:@"{0},viewMode:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/view-mode?value

            /*
             * Checking if [format] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/format?value
              set:x:/@_arguments?value
                src:@"{0},format:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/format?value

            /*
             * Checking if [min-date] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/min-date?value
              set:x:/@_arguments?value
                src:@"{0},minDate:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/min-date?value.string

            /*
             * Checking if [max-date] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/max-date?value
              set:x:/@_arguments?value
                src:@"{0},maxDate:'{1}'"
                  :x:/@_arguments?value
                  :x:/../*/_options/*/max-date?value.string

            /*
             * Checking if [show-today-button] was passed in, and if so, making sure we use it.
             */
            if:x:/../*/_options/*/show-today-button?value
              set:x:/@_arguments?value
                src:@"{0},showTodayButton:true"
                  :x:/@_arguments?value

            /*
             * Making sure we nicely format arguments.
             * Notice, at this point, the [_arguments] are starting with an additional ",".
             * Hence, here we are making sure we remove this additional comma.
             */
            if:x:/../*/_arguments?value
              p5.string.trim:x:/../*/_arguments?value
                chars:,
              set:x:/../*/_arguments?value
                src:"{{{0}}}"
                  :x:/@p5.string.trim?value

            /*
             * Initializing widget with JavaScript client inclusion.
             *
             * Notice, which widget to use to initialize datetimepicker, depends upon how the picker is displayed.
             * It can be either the main wrapper "div" element, or the "input" element itself, depending upon whether or not
             * an "open icon" was supplied.
             */
            _init-widget
            p5.web.widgets.get-parent:x:/../*/_event?value
            if
              fetch:x:/0/0/0?value
                p5.web.widgets.find-like:x:/../*/p5.web.widgets.get-parent/*/*?value
                  element:span
                  class:datetimepicker-dropdown

              /*
               * Using parent widget ("div" element) to initialize datetimepicker.
               */
              set:x:/../*/_init-widget?value
                src:x:/../*/p5.web.widgets.get-parent/*/*?value
            else

              /*
               * Using textbox for intializing datetimepicker.
               */
              set:x:/../*/_init-widget?value
                src:x:/../*/_event?value

            /*
             * Checking if we have an [.onchange] lambda callback.
             */
            p5.web.widgets.ajax-events.list:x:/../*/_event?value
            if:x:/@p5.web.widgets.ajax-events.list/*/*/.onchange

              /*
               * We have an [.onchange] lambda callback, making sure we intialize datetimepicker such that it raises [.onchange] when 
               * the widget is hidden.
               */
              p5.web.send-javascript:@"$('#{0}').datetimepicker({1}).on('dp.hide',function(){{p5.$('{2}').raise('.onchange');}});"
                :x:/../*/_init-widget?value
                :x:/../*/_arguments?value
                :x:/../*/_event?value

            else

              /*
               * We do not have an [.onchange] lambda callback.
               */
              p5.web.send-javascript:@"$('#{0}').datetimepicker({1});"
                :x:/../*/_init-widget?value
                :x:/../*/_arguments?value

          .onchange

            /*
             * Getting datetimepicker's value, and invoking [.onchange]'s callback.
             */
            p5.web.widgets.property.get:x:/../*/_event?value
              value
            p5.web.widgets.get-parent:x:/../*/_event?value
            .lambda
            eval-x:x:/+/*
            eval:x:/@.lambda
              _value:x:/@p5.web.widgets.property.get/*/*?value
              _event:x:/@p5.web.widgets.get-parent/*/*?value

        /*
         * Icon button for showing DateTimePicker
         */
        span
          class:input-group-addon datetimepicker-dropdown
          innerValue:@"<span class=""{0}""></span>"
            :x:/../*/_options/*/icon?value
