
/*
 * Creates the Active Event that toggles one or more CSS class(es) on widget.
 * This Active Event requires one or more CSS class(es) as [class], and will append the specified CSS class(es) to the widget defined as [_arg],
 * if the class does not already exist on widget, or remove the class from widget if it already exist from before.
 */
create-event:sys42.utilities.toggle-css-classes

  /*
   * Splitting upon every given CSS class caller wants to toggle.
   */
  p5.string.split:x:/../*/class?value
    =:,
    =:" "
    trim:true

  /*
   * Retrieving CSS class of specified widget.
   */
  p5.web.widgets.property.get:x:/../*/_arg?value
    class

  /*
   * Splitting each CSS class already in widget, such that we can figure out if class(es)
   * caller wants to toggle already exists or not.
   */
  p5.string.split:x:/@p5.web.widgets.property.get/*/*?value
    =:" "

  /*
   * Looping through each class supplied.
   */
  for-each:x:/../*/p5.string.split/[0,1]/*/$

    /*
     * Checking if currently iterated CSS class is in [p5.string.split] result.
     */
    if:x:/@p5.string.split/*/{0}
      :x:/@_dp/#?name

      /*
       * Class exists, deleting it.
       */
      set:x:/@p5.string.split/*/{0}
        :x:/@_dp/#?name

    else

      /*
       * Class does NOT exist, adding it.
       */
      add:x:/@p5.string.split
        src:x:/@_dp/#?name

  /*
   * Invoking [p5.string.join] on p5.string.split results, before setting new class value.
   */
  p5.string.join:x:/@p5.string.split/*?name
    sep:" "
  p5.web.widgets.property.set:x:/../*/_arg?value
    class:x:/@p5.string.join?value
