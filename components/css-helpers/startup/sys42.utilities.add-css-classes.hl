
/*
 * Creates the Active Event that adds a CSS class(es) to a widget.
 * This Active Event takes a CSS class(es) as [class], and will append that CSS class to the widget given as [_arg],
 * if the class does not already exist on widget.
 */
create-event:sys42.utilities.add-css-classes

  /*
   * Splitting upon every given CSS class caller wants to add.
   */
  split:x:/../*/class?value
    =:,
    =:" "
    trim:true

  /*
   * Retrieving CSS class of specified widget.
   */
  p5.web.widgets.property.get:x:/../*/_arg?value
    class

  /*
   * Splitting each CSS class already in widget, such that we can figure out if class(es)
   * caller wants to add already exists or not.
   */
  split:x:/@p5.web.widgets.property.get/*/*?value
    =:" "

  /*
   * Looping through each class supplied.
   */
  for-each:x:/../*/split/[0,1]/*/$

    /*
     * Checking if currently iterated CSS class is not already in [split] result.
     */
    if:x:/@split/*/{0}
      :x:/@_dp/#?name
      not

      /*
       * Class does NOT exist, adding it
       */
      add:x:/@split
        src:x:/@_dp/#?name

  /*
   * Invoking [join] on split results, before settings new class value
   */
  join:x:/@split/*?name
    sep:" "
  p5.web.widgets.property.set:x:/../*/_arg?value
    class:x:/@join?value
