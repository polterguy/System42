
/*
 * File responsible for initializing System42.
 * Invoked by the P5 core, due to ".p5.webapp.application-startup-file" setting in web.config.
 *
 * Creates three Active Events. Active events this file creates are as follows;
 *
 * [p5.io.unroll-path.@system42] - Which evaluates to the "/system42/" folder, and is used as a "path variable".
 * [sys42.utilities.execute-lambda-file] - Evaluates one or more Hyperlambda files.
 * [sys42.utilities.execute-lambda-folder] - Evaluates all Hyperlambda files inside all specified folders.
 *
 * If you want a completely clean install, without any of the System42 apps or components, then delete the "system42"
 * folder, and change your ".p5.webapp.application-startup-file" web.config setting, to evaluate another file upon startup.
 * If you do, you must also provide your own [p5.web.load-ui] Active Event.
 */


/*
 * Creates the Active Event that points to our "/system42/" folder.
 */
create-event:p5.io.unroll-path.@SYS42
  return:/system42


/*
 * Creates the Active Event that allows you to execute Hyperlambda p5 lambda files. 
 *
 * The Hyperlambda file(s) you wish to execute is given as [_arg], or the value of your [sys42.utilities.execute-lambda-file] invocation,
 * and all nodes benath [sys42.utilities.execute-lambda-file] when invoked, will be passed into your p5 lambda file execution.
 *
 * File to exeute can be given either as an expression, or a constant. Multiple files can be executed at the same time, 
 * by providing an expression that leads to multiple results.
 */
create-event:sys42.utilities.execute-lambda-file

  /*
   * Loading up all files given as [_arg] arguments.
   */
  load-file:x:/../*/_arg?value

  /*
   * Forwarding all arguments given to evaluation of file.
   * This expression will add up all nodes before our [load-file] node, except the [_arg] argument, which basically means all arguments 
   * passed into event invocation, except [_arg], which is the file to evaluate.
   */
  add:x:/+
    src:x:/./-/--!/../*/_arg

  /*
   * Evaluating [load-file]'s children, with the arguments applied in the above [add] invocation.
   */
  eval:x:/@load-file/*

  /*
   * Returning the results of the above evaluation to caller.
   * First the nodes returned by the evaluation, and then the value of the root node of the invocation.
   *
   * Notice, this means that if you invoke multiple files in one go, only the last returned value will actually be returned
   * from this event. The last value returned from the files evaluated above, will simply overwrite any previous values returned.
   */
  insert-before:x:
    src:x:/@eval/*
  set:x:/..?value
    src:x:/@eval?value


/*
 * Evaluates all Hyperlambda files within the specified directory(ies) recursively.
 *
 * The directory you wish to evaluate, is given as the value of the [sys42.utilities.execute-lambda-folder] node, 
 * which might be either an expression, or a constant. You can pass in multiple folders in one go, 
 * by using an expression leading to multiple folders as your argument.
 *
 * Notice, this event will not be able to return anything to caller, that was returned from the files' invocations.
 */
create-event:sys42.utilities.execute-lambda-folder

  /*
   * Listing all Hyperlambda files in directories we're given.
   */
  list-files:x:/../*/_arg?value
    filter:.hl

  /*
   * Evaluating all Hyperlambda files from that folder.
   */
  sys42.utilities.execute-lambda-file:x:/@list-files/*?name

  /*
   * Listing all folders recursively, within the folder(s) we've just evaluated.
   */
  list-folders:x:/../*/_arg?value

  /*
   * If the above [list-folder] gave us any result(s), we recursively invoke self.
   */
  if:x:/-/*
    sys42.utilities.execute-lambda-folder:x:/@list-folders/*?name


/*
 * After creating the above Active Events, this file will evaluate all files in the "/system42/startup/" 
 * folder recursively, by using the Active Events above.
 */
sys42.utilities.execute-lambda-folder:@SYS42/startup/
